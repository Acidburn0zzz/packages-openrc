# Maintainer: udeved <udeved@openrc4arch.site40.net>

pkgbase=openrc-apps
pkgname=openrc-apps
true && pkgname=('alsa-utils-openrc' 'cpupower-openrc' 'cronie-openrc' 'gpm-openrc' 'lm_sensors-openrc' 'sane-openrc' 'syslog-ng-openrc')
pkgver=20131016
pkgrel=1
pkgdesc="OpenRC init scripts"
arch=('any')
url="http://openrc4arch.site40.net"
license=('GPL2')
groups=('openrc-apps')
makedepends=('alsa-utils' 'cpupower' 'cronie' 'gpm' 'lm_sensors' 'sane' 'syslog-ng')
conflicts=('openrc-arch-services-git')

_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"

source=("${_gentoo_uri}/media-sound/alsa-utils/files/alsasound.confd-r4"
	"${_gentoo_uri}/media-sound/alsa-utils/files/alsasound.initd-r5"
	"${_gentoo_uri}/sys-power/cpupower/files/conf.d-r2"
	"${_gentoo_uri}/sys-power/cpupower/files/init.d-r2"
	"${_gentoo_uri}/sys-process/cronie/files/cronie-1.3-initd"
	"${_gentoo_uri}/sys-process/cronie/files/anacron-1.0-initd"
	"${_gentoo_uri}/sys-libs/gpm/files/gpm.conf.d"
	"${_gentoo_uri}/sys-libs/gpm/files/gpm.rc6-2"
	"${_gentoo_uri}/sys-apps/lm_sensors/files/sensord-conf.d"
	"${_gentoo_uri}/sys-apps/lm_sensors/files/sensord-4-init.d"
	"${_gentoo_uri}/sys-apps/lm_sensors/files/fancontrol-init.d-2"
	"${_gentoo_uri}/sys-apps/lm_sensors/files/lm_sensors-3-init.d"
	"${_gentoo_uri}/media-gfx/sane-backends/files/saned.confd"
	"${_gentoo_uri}/media-gfx/sane-backends/files/saned.initd"
	"${_gentoo_uri}/app-admin/syslog-ng/files/syslog-ng.confd"
	"${_gentoo_uri}/app-admin/syslog-ng/files/syslog-ng.rc6.3.3")

sha256sums=('d1c55400b701a72dcb8bb85e016b5013fa3eb6a2766ffc20dae278d0ee4c1a43'
            'daf46bbc4156a82e0c6c7135ee94b31a3dfb6d366fcf3f45e8c842d1348601be'
            '9ab6f022d2b2948660decf5e383984e6ddb9e9e5e6e2761c3031378ddd87e947'
            '8d8e45f112ea86fb686f66ca16c25f6d1b0e06c62b0fd2ce2ed39b455a1d8a3c'
            '0954be9e1e1c34d2f963cc60828b63711a2949bfe91f65620caf9c80256383f2'
            '862c85322f3644997ca5432180e0ef0a4fb36467a44b350dc955365f314b2fbe'
            '73e7483fdc4b12ab4225a4cb13bbe7da71b07b9e69b17e3a6a4c63cb5e2287c8'
            'e692e7b97efdd79f6e92fbdaed60f7a71bfd23a82c5561b160b88a7aa50c8461'
            '02230edf5efbb0a4de3840cd76f9ebf431c57a246fa7388e9dbeb0fe26fe7b8d'
            'e75915a13d3c5eb405e2809186a6ab396b2e0180c3efc94e663f9eb4e276e8ef'
            '36d489296c31736f8015b0ce27052b3f1555b7fe6335120c0477b044b8e4fb8d'
            '9b018f9f7a0975988387858823fe59a5cd8af6413d8c3170db0e24aac6021ec3'
            '197e44ba1f438a18f5f7d9f5858feb19c1ece4286d82a5e63caf9be5b964aa76'
            '4dd4e7fa07bf2ab2d4f5753156f5df0ad2277523f6755b0eab3d2db3480989e2'
            '8319ca8e39a5dab5ddc82eede088e1f58ff25deef330804648000359cb736a3f'
            '19bbd35abc9f54ce368e1ccf46b6e4af6eec304906e432bb9968d50a01d18284')

pkgver() {
  date +%Y%m%d
}

package_alsa-utils-openrc() {
	true
	pkgdesc="OpenRC alsa-utils init script"
	depends=('openrc' 'alsa-utils')
	install=alsa-utils.install

	install -Dm755 "${srcdir}/alsasound.confd-r4" "${pkgdir}/etc/conf.d/alsasound"
	install -Dm755 "${srcdir}/alsasound.initd-r5" "${pkgdir}/etc/init.d/alsasound"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -i "${pkgdir}/etc/init.d/alsasound"
}

package_cpupower-openrc() {
	true
	pkgdesc="OpenRC cpupower init script"
	depends=('openrc' 'cpupower')
	install=cpupower.install

	install -Dm755 "${srcdir}/conf.d-r2" "${pkgdir}/etc/conf.d/cpupower"
	install -Dm755 "${srcdir}/init.d-r2" "${pkgdir}/etc/init.d/cpupower"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -i "${pkgdir}/etc/init.d/cpupower"
}

package_cronie-openrc() {
	true
	pkgdesc="OpenRC cronie init script"
	depends=('openrc' 'cronie')
	provides=('openrc-cron')
	conflicts=('openrc-cron')
	install=cronie.install

	install -Dm755 "${srcdir}/cronie-1.3-initd" "${pkgdir}/etc/init.d/cronie"
	install -Dm755 "${srcdir}/anacron-1.0-initd" "${pkgdir}/etc/init.d/anacron"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/cronie"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/anacron"
}

package_gpm-openrc() {
	true
	pkgdesc="OpenRC gpm init script"
	depends=('openrc' 'gpm')
	install=gpm.install

	install -Dm755 "${srcdir}/gpm.conf.d" "${pkgdir}/etc/conf.d/gpm"
	install -Dm755 "${srcdir}/gpm.rc6-2" "${pkgdir}/etc/init.d/gpm"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/gpm"
}

package_lm_sensors-openrc() {
	true
	pkgdesc="OpenRC lm_sensors init script"
	depends=('openrc' 'lm_sensors')
	install=ln_sensors.install

	install -Dm755 "${srcdir}/sensord-conf.d" "${pkgdir}/etc/conf.d/sensord"
	install -Dm755 "${srcdir}/sensord-4-init.d" "${pkgdir}/etc/init.d/sensord"
	install -Dm755 "${srcdir}/fancontrol-init.d-2" "${pkgdir}/etc/init.d/fancontrol"
	install -Dm755 "${srcdir}/lm_sensors-3-init.d" "${pkgdir}/etc/init.d/lm_sensors"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i ${pkgdir}/etc/init.d/sensord
	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i ${pkgdir}/etc/init.d/fancontrol
        sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i ${pkgdir}/etc/init.d/lm_sensors
}

package_sane-openrc() {
	true
	pkgdesc="OpenRC sane init script"
	depends=('openrc' 'sane')
	install=sane.install

	install -Dm755 "${srcdir}/saned.confd" "${pkgdir}/etc/conf.d/saned"
	install -Dm755 "${srcdir}/saned.initd" "${pkgdir}/etc/init.d/saned"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/var/run|/run|' -e 's|/usr/sbin|/usr/bin|' -i ${pkgdir}/etc/init.d/saned
}

package_syslog-ng-openrc() {
	true
	pkgdesc="OpenRC syslog-ng init script"
	depends=('openrc' 'syslog-ng')
	install=syslog-ng.install

	install -Dm755 "${srcdir}/syslog-ng.confd" "${pkgdir}/etc/conf.d/syslog-ng"
	install -Dm755 "${srcdir}/syslog-ng.rc6.3.3" "${pkgdir}/etc/init.d/syslog-ng"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i ${pkgdir}/etc/init.d/syslog-ng
}