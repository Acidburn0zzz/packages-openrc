# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

_name="syslog-ng"
_cat="app-admin"
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/${_cat}/${_name}/files"

pkgname=syslog-ng-openrc
pkgver=3.4.3
pkgrel=2
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('openrc-misc')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('awk' 'eventlog' 'glib2' 'libcap' 'openrc' 'openssl' 'udev')
makedepends=('flex' 'pkg-config' 'python2')
optdepends=('logrotate: for rotating log files')
provides=('syslog-ng')
conflicts=('syslog-ng')
options=('!libtool')
backup=('etc/syslog-ng/scl.conf'
        'etc/syslog-ng/syslog-ng.conf'
        'etc/logrotate.d/syslog-ng')
install=syslog-ng.install
source=("http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/${_name}_$pkgver.tar.gz"
        'syslog-ng.conf'
        'syslog-ng.logrotate'
        "${_gentoo_uri}/syslog-ng.confd"
	"${_gentoo_uri}/syslog-ng.rc6.3.3")

build() {
  cd "${_name}-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/syslog-ng \
    --libexecdir=/usr/lib \
    --sbindir=/usr/bin \
    --localstatedir=/var/lib/syslog-ng \
    --datadir=/usr/share/syslog-ng \
    --with-pidfile-dir=/run \
    --disable-spoof-source \
    --enable-ipv6 \
    --disable-systemd

  make
}

package() {
  make -C "${_name}-$pkgver" DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"

  install -Dm755 "${srcdir}/syslog-ng.confd" "${pkgdir}/etc/conf.d/syslog-ng"
  install -Dm755 "${srcdir}/syslog-ng.rc6.3.3" "${pkgdir}/etc/init.d/syslog-ng"
  _fix_init "${pkgdir}/etc/init.d/syslog-ng"
}

_fix_init(){
  sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i $1
}
sha256sums=('8a00c342036109e6cc1d441aaed12438134bc884ca3492c36902ff9120a107ee'
            '0be3d8b5b5de79392a67ed6a074e562b542beab117cd608529a6a56af678b6f8'
            '93c935eca56854011ea9e353b7a1da662ad40b2e8452954c5b4b5a1d5b2d5317'
            '8319ca8e39a5dab5ddc82eede088e1f58ff25deef330804648000359cb736a3f'
            '19bbd35abc9f54ce368e1ccf46b6e4af6eec304906e432bb9968d50a01d18284')
