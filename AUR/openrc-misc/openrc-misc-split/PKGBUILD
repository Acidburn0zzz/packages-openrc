# Maintainer: udeved <udeved@openrc4arch.site40.net>

pkgbase=openrc-misc
pkgname=openrc-misc
true && pkgname=('fcron-openrc' 'openntpd-openrc' 'rsyslog-openrc'
		'vbox-guest-openrc')
pkgver=20131015
pkgrel=1
pkgdesc="OpenRC init scripts"
arch=('any')
url="http://openrc4arch.site40.net"
license=('GPL2')
#groups=('openrc-misc')
makedepends=('fcron' 'openntpd' 'rsyslog'
	    'virtualbox-guest-additions')
conflicts=('openrc-arch-services-git')

_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"

source=("${_gentoo_uri}/sys-process/fcron/files/fcron.init.3"
	"${_gentoo_uri}/net-misc/openntpd/files/openntpd.conf.d-3.9_p1-r4"
	"${_gentoo_uri}/net-misc/openntpd/files/openntpd.init.d-20080406-r2"
	"${_gentoo_uri}/app-admin/rsyslog/files/7-stable/rsyslog.confd"
	"${_gentoo_uri}/app-admin/rsyslog/files/7-stable/rsyslog.initd"
	"${_gentoo_uri}/app-emulation/virtualbox-guest-additions/files/virtualbox-guest-additions-8.initd")

pkgver() {
  date +%Y%m%d
}

package_fcron-openrc() {
	true
	pkgdesc="OpenRC fcron init script"
	depends=('openrc' 'fcron')
	provides=('openrc-cron')
	conflicts=('openrc-cron')
	install=fcron.install

	install -Dm755 "${srcdir}/fcron.init.3" "${pkgdir}/etc/init.d/fcron"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/libexec|/usr/bin|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/fcron"

}

package_openntpd-openrc() {
	true
	pkgdesc="OpenRC openntpd init script"
	depends=('openrc' 'openntpd')
	provides=('openrc-timed')
	conflicts=('openrc-timed')
	install=openntpd.install

	install -Dm755 "${srcdir}/openntpd.conf.d-3.9_p1-r4" "${pkgdir}/etc/conf.d/openntpd"
	install -Dm755 "${srcdir}/openntpd.init.d-20080406-r2" "${pkgdir}/etc/init.d/openntpd"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/openntpd"
}

package_rsyslog-openrc() {
	true
	pkgdesc="OpenRC rsyslog init script"
	depends=('openrc' 'rsyslog')
	install=rsyslog.install

	install -Dm755 "${srcdir}/rsyslog.confd" "${pkgdir}/etc/conf.d/rsyslog"
	install -Dm755 "${srcdir}/rsyslog.initd" "${pkgdir}/etc/init.d/rsyslog"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/rsyslog"

}

package_vbox-guest-openrc() {
	true
	pkgdesc="OpenRC virtualbox-guest-utils init script"
	depends=('openrc' 'virtualbox-guest-utils')
	install=virtualbox-guest-additions.install

	install -Dm755 "${srcdir}/virtualbox-guest-additions-8.initd" "${pkgdir}/etc/init.d/vboxservice"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -e 's|vboxguest-service|VBoxService|g' -i "${pkgdir}/etc/init.d/vboxservice"

}
