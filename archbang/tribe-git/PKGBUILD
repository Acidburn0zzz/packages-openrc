# Maintainer: udeved <udeved@openrc4arch.site40.net>

_codename=Grasshopper
_pkgname=tribe

pkgname=tribe-git
pkgver=r9.edeb2a3
pkgrel=1
pkgdesc="Archbang LiveCD Installer forked from chakra"
url="https://github.com/udeved/tribe"
license="GPL"
arch=('i686' 'x86_64')
provides=('tribe')
conflicts=('tribe')
depends=('kdelibs' 'kdeedu-marble>=4.10.0' 'squashfs-tools' 'tribe-partitionmanager'
        'rsync' 'mkinitcpio-nfs-utils' 'ntfs-3g' 'libpwquality')
makedepends=('cmake' 'kdebase-workspace' 'automoc4')
source=('git+https://github.com/udeved/tribe.git'
        'launch-tribe.sh')
md5sums=('SKIP'
         'e31855da237a2c8029774831b3c04685')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "${srcdir}/${_pkgname}"
  sed -i -e "s/@ARCH@/${CARCH}/g" src/config-tribe.h.cmake
  sed -i -e "s~source build/from git~Build: ${pkgver} ($_codename)~g" src/config-tribe.h.cmake
  sed -i -e "s~code-name~$_codename~g" "scripts/postinstall-functions/01-job-initialize-target"
  sed -i -e "s~chakra-version~${pkgver}~g" "scripts/postinstall-functions/01-job-initialize-target"
}

build()
{
  cd "${srcdir}/${_pkgname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_SKIP_RPATH=ON \
	  -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
  make
}

package()
{
  cd "${srcdir}/${_pkgname}"
  make DESTDIR=${pkgdir} install
  install -Dm755 ${srcdir}/launch-tribe.sh \
  ${pkgdir}/usr/bin/launch-tribe.sh
  #cp -vf ${srcdir}/RELEASE_NOTES_${_rls_notes}.html $pkgdir/usr/share/tribe/config/RELEASE_NOTES.html

  find ${pkgdir}/ -name ".git" -type d -exec rm -fr {} +
}

