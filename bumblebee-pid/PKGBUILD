# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Roland Singer <roland@manjaro.org>
# Maintainer: Philip Mueller <philm@manjaro.org>

_pkgname=bumblebee

pkgname=bumblebee-pid
pkgver=3.2.1
pkgrel=1
pkgdesc="NVIDIA Optimus support for Linux through Primus/VirtualGL"
arch=('i686' 'x86_64')
depends=('primus' 'glib2' 'mesa-libgl')
makedepends=('help2man' 'libbsd')
optdepends=('xf86-video-nouveau: nouveau driver'
            'nouveau-dri: 3D acceleration features fo nouveau'
            'bbswitch: switch on/off discrete card'
            'nvidia: NVIDIA kernel driver'
            'virtualgl: alternative back-end for optirun')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-primus: faster 32bit back-end for optirun'
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi
conflicts=('bumblebee')
provides=('bumblebee')
url="http://www.bumblebee-project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
        'etc/bumblebee/xorg.conf.nvidia')
source=("http://www.bumblebee-project.org/${_pkgname}-${pkgver}.tar.gz"
		'bumblebee-3.2.1-modprobe.patch::https://github.com/Bumblebee-Project/Bumblebee/commit/1ada79fe5916961fc4e4917f8c63bb184908d986.patch')
sha256sums=('1018703b07e2f607a4641249d69478ce076ae5a1e9dd6cff5694d394fa7ee30e'
            'faca07b50f0a3ea3fdba74cc72e0e0504a237324698ccb7033c0586d38dc3875')

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    patch -p1 -i "${srcdir}/bumblebee-3.2.1-modprobe.patch"
}

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
        CONF_GID=users \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-udev-rules=/usr/lib/udev/rules.d/ \
        --sysconfdir=/etc

    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    make install DESTDIR="$pkgdir"

    # Install systemd unit
    install -D -m644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"
    sed -i "s/sbin/bin/" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"

    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"

    # Adjust ServerGroup to users
    sed -i "/#/!s|ServerGroup[ ]*=.*|ServerGroup=users|g" "${pkgdir}/etc/bumblebee/bumblebee.conf"
}
