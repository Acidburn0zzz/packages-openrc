# $Id: PKGBUILD 191316 2013-07-23 14:05:15Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord-eudev
pkgver=1.0.2
pkgrel=1
pkgdesc="System daemon for managing color devices"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lcms2 libgusb polkit shared-color-profiles sqlite udev dconf dbus)
makedepends=(intltool gobject-introspection vala docbook2x sane bash-completion)
optdepends=('sane: UseSANE support [/etc/colord.conf]')
provides=('colord')
conflicts=('colord')
options=('!libtool')
install=colord.install
backup=('etc/colord.conf')
source=("$url/releases/colord-$pkgver.tar.xz")
	#'udev-systemd.patch')

# prepare() {
#   cd colord-$pkgver
#   patch -Np1 -i ../udev-systemd.patch
# }

build() {
  cd colord-$pkgver

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/colord \
    --disable-static \
    --enable-vala \
    --enable-sane \
    --disable-volume-search \
    --disable-systemd-login
  make
}

package() {
  cd colord-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
sha256sums=('b99d1d07fe21a37b446aa173a35c54fd669324ad61762a7545b1624c896a8a39')
           # '6c3e4317414573b48a4f056d948c14e61e60b630d08db7e7d57f5480c87a9749'
