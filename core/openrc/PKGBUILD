# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: williamh <williamh@gentoo.org>

pkgname=openrc
pkgver=0.11.8
pkgrel=11
pkgdesc="OpenRC universal init system."
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD')
groups=('openrc-base')
depends=('bash'
	'coreutils'
	'glibc'
	'inetutils'
	'ncurses'
	'net-tools'
	'pam'
	'psmisc'
	'sysvinit')
optdepends=('openrc-virtual-syslog'
	'openrc-virtual-cron')
conflicts=('initscripts'
	"$pkgname-git" 'openrc4arch')
backup=('etc/rc.conf'
	'etc/inittab'
	'etc/conf.d/consolefont'
	'etc/conf.d/keymaps'
	'etc/conf.d/hwclock'
	'etc/conf.d/hostname'
	'etc/conf.d/modules')
source=("http://dev.gentoo.org/~williamh/dist/$pkgname-$pkgver.tar.bz2"
	'consolefont.patch'
	'openrc.logrotate'
	'runlevel.patch')
md5sums=('d896371d533a22de4cf57c7371c4d6e8'
         'd21ffb2a2d7d5e511daca45d746c2675'
         'ede356beae529d1b16b769c9da70ad52'
         'd1a5e62f0668c787a83cf9fe392a39cd')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	msg "patching ..."
		# both issues fixed in git
		patch -p1 -i $srcdir/consolefont.patch
		patch -p1 -i $srcdir/runlevel.patch
	msg "patching done"
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make \
		SYSCONFDIR='/etc' \
		OS='Linux' \
		BRANDING='Arch Linux' \
		PREFIX='/usr' \
		LIBEXECDIR='/usr/lib/rc' \
		MKSELINUX='no' \
		MKPAM='pam' \
		MKTERMCAP='ncurses' \
		MKNET='oldnet' \
		DESTDIR="$pkgdir" \
		install

	install -m 644 "$srcdir/$pkgname-$pkgver/support/sysvinit/inittab" "$pkgdir/etc/inittab"

	install -Dm 0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"

	install -m 644 "$srcdir/$pkgname-$pkgver/conf.d/net" "$pkgdir/etc/conf.d/net"
	
	mkdir "$pkgdir/sbin"
	ln -s "/usr/sbin/rc" "$pkgdir/sbin/rc"
}
