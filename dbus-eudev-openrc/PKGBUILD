# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

_name="dbus"
_cat="sys-apps"
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/${_cat}/${_name}/files"

pkgname=dbus-eudev-openrc
pkgver=1.6.16
pkgrel=1
pkgdesc="Freedesktop.org message bus system with openrc support and disabled systemd"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
groups=('openrc-sys')
depends=('expat' 'coreutils' 'filesystem' 'openrc-base')
makedepends=('libx11' 'udev')
optdepends=('libx11: dbus-launch support'
	    'upower-eudev: upower without systemd'
	    'udisks2-eudev: udisks2 without systemd')
provides=('dbus-core' 'dbus' 'dbus-openrc')
conflicts=('dbus-core''dbus' 'dbus-openrc' 'dbus-eudev')
replaces=('dbus-core' 'dbus-openrc' 'dbus-eudev')
install=${_name}.install
options=(!libtool)
source=("http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz"
	"${_gentoo_uri}/dbus.initd"
        '30-dbus')

sha256sums=('463f42873aafcde733de3105e6d166709a42bc961fb4e3ef27657dfd55813573'
            '98e37b8b6ed25004e48c5855d74c9361eea06d3fee13cefcc0ed10ccf452aa01'
            'a1c324ae758046f0c3ef46db02ab873d35adeaab9980c2e0ee293accb89d0d2c')

build() {
  cd dbus-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/dbus-1.0 \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-console-auth-dir=/run/console/ \
    --enable-inotify \
    --disable-dnotify \
    --disable-verbose-mode \
    --disable-static \
    --disable-tests  \
    --disable-asserts \
    --disable-systemd

  make
}

package(){
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/var/run"

  install -Dm755 ${srcdir}/30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"

  install -Dm755 "${srcdir}/dbus.initd" "${pkgdir}/etc/init.d/dbus"

  sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|dbus.pid|dbus/pid|g' -e 's|/var/run|/run|g' -i "${pkgdir}/etc/init.d/dbus"
}
