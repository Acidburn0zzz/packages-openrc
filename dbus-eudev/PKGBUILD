# Maintainer: artoo <artoo@manjaro.org>

pkgname=dbus-eudev
pkgver=1.8.16
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
groups=('eudev-base')
depends=('libdbus' 'expat')
makedepends=('libx11' 'eudev' 'xmlto' 'docbook-xsl')
optdepends=('libx11: dbus-launch support')
provides=('dbus-core' "dbus=${pkgver}")
conflicts=('dbus-core' 'dbus')
#replaces=('dbus-core' 'dbus')
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz #{,.asc}
        30-dbus)

build() {
  cd dbus-$pkgver
  ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/dbus-1.0 \
		--with-dbus-user=dbus \
		--with-system-pid-file=/run/dbus/pid \
		--with-system-socket=/run/dbus/system_bus_socket \
		--with-console-auth-dir=/run/console/ \
		--enable-inotify \
		--disable-dnotify \
		--disable-verbose-mode \
		--disable-static \
		--disable-tests --disable-asserts \
		--disable-systemd
  make
}

package(){
  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}/var/run"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"

  install -Dm755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus.sh"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}
sha256sums=('7f795268efd343ff0498786acb35097564390d40b1b6158daf1b4f742b522b07'
            'dc1ce6d38674bad7a48ad1911576f8bbb3819f1019126fb1ef7c3cfad16bb02a')
