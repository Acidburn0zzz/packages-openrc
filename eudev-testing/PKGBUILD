# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=212

pkgbase=eudev
pkgname=('eudev' 'libeudev')
pkgver=1.6
pkgrel=2
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
makedepends=('gobject-introspection'
			'gperf'
			'gtk-doc'
			'intltool')
options=(!makeflags !libtool)
source=("${url}/${pkgbase}-${pkgver}.tar.gz"
		'initcpio_hooks'
		'initcpio_install')

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
		--with-modprobe=/usr/bin/modprobe \
		--with-html-dir=/usr/share/doc/${pkgbase}-${pkgver}/html \
		--enable-doc \
		--enable-gtk-doc \
		--enable-gudev \
		--enable-introspection \
		--enable-split-usr \
		--enable-rule-generator
	make
}

package_eudev() {
	pkgdesc="The userspace dev tools (udev) forked by Gentoo."
	license=('GPL')
	groups=('eudev-base')
	depends=('glib2'
			'hwids'
			'kbd'
			'kmod'
			'util-linux'
			"libeudev=${_udevver}")
	optdepends=('eudev-openrc: eudev-postmount script'
		    'upower-nosystemd: upower without systemd'
		    'udisks2-nosystemd: udisks2 without systemd')
	provides=("udev=${_udevver}"
			"systemd=${_udevver}"
			"systemd-tools=${_udevver}")
	conflicts=('systemd'
			'systemd-tools')
	replaces=('systemd'
			'systemd-tools')
	backup=('etc/udev/udev.conf')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rm -rf "${srcdir}/_libeudev"

	install -dm755 "${srcdir}/_libeudev/usr/lib"
	cd "${srcdir}/_libeudev"
	mv "${pkgdir}/usr/lib"/libudev*.so* usr/lib
	mv "${pkgdir}/usr/lib"/libgudev*.so* usr/lib
}

package_libeudev() {
	pkgdesc="eudev libraries"
	depends=('glib2' 'glibc')
	license=('GPL')
	provides=('libgudev-1.0.so' 'libudev.so' "libsystemd=${_udevver}")
	conflicts=('libsystemd')
	replaces=('libsystemd')

	mv "${srcdir}/_libeudev"/* "${pkgdir}"
}

sha256sums=('9d00d6925ccc7763be89472c5058012bd45ce13f14297e40e4cf3a02244a6a47'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1a70e48d5f5b476ed624f745b03988f2e8950e33ae864c6b1c4ef088882c8390')
