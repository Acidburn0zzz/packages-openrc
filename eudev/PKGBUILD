# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>
# Contributor: Aaditya Bagga <aaditya_gnulinux@zoho.com>

_udev_ver=217
_src_uri='http://dev.gentoo.org/~blueness/eudev'

pkgname=eudev
pkgver=3.0
pkgrel=3
pkgdesc="The userspace dev tools (udev) forked by Gentoo"
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/eudev/"
license=('GPL')
depends=('glib2' 'kbd' 'kmod' 'hwids' 'util-linux')
makedepends=('gobject-introspection' 'gperf' 'gtk-doc' 'intltool')
optdepends=('eudev-systemdcompat: makes packages compiled with systemd features run'
	    'upower-pm-utils: pm-utils support')
provides=("udev=${_udev_ver}"
	"libudev=${_udev_ver}"
	'libgudev-1.0.so'
	'libudev.so')
conflicts=('libgudev-1.0.so' 'libudev.so')
backup=('etc/udev/udev.conf')
options=(!makeflags !libtool)
source=("https://github.com/gentoo/eudev/archive/v${pkgver}.tar.gz"
	'initcpio_hooks'
	'initcpio_install')
sha256sums=('7c480fb77feb94f14a52bbd3d1e84fca3afe83548aba5297a368016cb6adadfd'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1a70e48d5f5b476ed624f745b03988f2e8950e33ae864c6b1c4ef088882c8390')
            
build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-html-dir=/usr/share/doc/${pkgname}-${pkgver}/html \
		--enable-gudev \
		--enable-introspection \
		--enable-kmod \
		--enable-split-usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
		sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
                            s#GROUP="tape"#GROUP="storage"#g;
                            s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
	done

	# input group is not used in Arch Linux at this moment
	sed '/GROUP="input"/d' -i "${pkgdir}/usr/lib/udev/rules.d/50-udev-default.rules"

	install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
	install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
}
