# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=212

pkgname=eudev
pkgver=1.6
pkgrel=2
pkgdesc="The userspace dev tools (udev) forked by Gentoo"
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
license=('GPL')
groups=('eudev-base')
depends=('glib2' 'hwids' 'kbd' 'kmod' 'util-linux')
makedepends=('gobject-introspection'
	    'gperf'
	    'gtk-doc'
	    'intltool')
optdepends=('eudev-openrc: eudev-postmount script'
	    'upower-nosystemd: upower without systemd'
	    'udisks2-nosystemd: udisks2 without systemd')
provides=("udev=${_udevver}"
	"systemd=${_udevver}"
	"libsystemd=${_udevver}"
	"systemd-tools=${_udevver}")
conflicts=('systemd'
	'libsystemd'
	'systemd-tools')
backup=('etc/udev/udev.conf')
options=(!makeflags !libtool)
source=("${url}/${pkgname}-${pkgver}.tar.gz"
	"${_gentoo_uri}/sys-fs/eudev/files/udev-postmount"
	'initcpio_hooks'
	'initcpio_install')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --with-rootprefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --sbindir=/usr/bin \
    --with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
    --with-modprobe=/usr/bin/modprobe \
    --with-html-dir=/usr/share/doc/${pkgname}-${pkgver}/html \
    --enable-doc \
    --enable-gtk-doc \
    --enable-gudev \
    --enable-introspection \
    --enable-split-usr \
    --enable-rule-generator
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
		s#GROUP="tape"#GROUP="storage"#g;
		s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
}
sha256sums=('9d00d6925ccc7763be89472c5058012bd45ce13f14297e40e4cf3a02244a6a47'
            '0220e949b1f31832a205021c0a8615e77b27b73b7c9b16ef4a61bbd2774aeecb'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1a70e48d5f5b476ed624f745b03988f2e8950e33ae864c6b1c4ef088882c8390')
