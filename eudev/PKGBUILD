# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=212

pkgbase=eudev
pkgname=('eudev' 'libeudev')
pkgver=1.6
pkgrel=3
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
groups=('eudev-base')
makedepends=('acl' 'dbus' 'hwids' 'kbd' 'kmod' 'pam'
	    'util-linux' 'gobject-introspection'
	    'gperf' 'gtk-doc' 'intltool')
options=(!makeflags !libtool)
source=("${url}/${pkgbase}-${pkgver}.tar.gz"
	'initcpio_hooks'
	'initcpio_install')
sha256sums=('9d00d6925ccc7763be89472c5058012bd45ce13f14297e40e4cf3a02244a6a47'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1a70e48d5f5b476ed624f745b03988f2e8950e33ae864c6b1c4ef088882c8390')

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
		--with-modprobe=/usr/bin/modprobe \
		--with-html-dir=/usr/share/doc/${pkgbase}-${pkgver}/html \
		--enable-doc \
		--enable-gtk-doc \
		--enable-gudev \
		--enable-introspection \
		--enable-split-usr \
		--enable-rule-generator
	make
}

package_eudev() {
  pkgdesc="The userspace dev tools (udev) forked by Gentoo"
  license=('GPL')
  depends=('glib2' 'kbd' 'kmod' 'hwids' 
	    "libeudev=${_udevver}" 'pam' 'util-linux')
  optdepends=('eudev-openrc: eudev-postmount script'
	      'upower-nosystemd: upower without systemd'
	      'udisks2-nosystemd: udisks2 without systemd')
  provides=("udev=${_udevver}"
	    "systemd=${_udevver}"
	    "systemd-tools=${_udevver}")
  conflicts=('systemd'
	    'systemd-tools')
  backup=('etc/udev/udev.conf')	    
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
		s#GROUP="tape"#GROUP="storage"#g;
		s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
  
  ### split off runtime libraries
  rm -rf "$srcdir/_libeudev"
  install -dm755 "$srcdir"/_libeudev/usr/lib
  cd "$srcdir"/_libeudev
  mv "$pkgdir"/usr/lib/lib{gudev,udev}*.so* usr/lib
}

package_libeudev() {
  pkgdesc="eudev client libraries"
  depends=('glib2' 'glibc')
  license=('GPL2')
  provides=('libgudev-1.0.so' 'libudev.so'
	  "libeudev=${_udevver}"
	  "libsystemd=${_udevver}")	    
  conflicts=('libsystemd.so' 'libsystemd-daemon.so' 'libsystemd-id128.so'
            'libsystemd-journal.so' 'libsystemd-login.so')

  mv "$srcdir/_libeudev"/* "$pkgdir"
}
