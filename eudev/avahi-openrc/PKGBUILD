# $Id: PKGBUILD 191362 2013-07-24 02:13:43Z bisson $
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=avahi-openrc
pkgver=0.6.31
pkgrel=2
pkgdesc='Multicast/unicast DNS-SD framework'
url='http://www.avahi.org/'
license=('LGPL')
arch=('i686' 'x86_64')
groups=('openrc-net')
options=('!libtool' '!emptydirs')
depends=('expat' 'libdaemon' 'glib2' 'libcap' 'gdbm' 'dbus-openrc')
optdepends=('gtk3: avahi-discover-standalone, bshell, bssh, bvnc'
            'gtk2: gtk2 bindings'
            'qt3: qt3 bindings'
            'qt4: qt4 bindings'
            'pygtk: avahi-bookmarks, avahi-discover'
            'twisted: avahi-bookmarks'
            'mono: mono bindings'
            'python2-dbus: avahi-discover'
            'nss-mdns: NSS support for mDNS')
makedepends=('qt3' 'qt4' 'pygtk' 'mono' 'intltool' 'python2-dbus'
             'gtk-sharp-2' 'gobject-introspection' 'gtk3' 'xmltoman')
backup=('etc/avahi/hosts'
        'etc/avahi/avahi-daemon.conf'
        'etc/avahi/services/ssh.service'
        'etc/avahi/services/sftp-ssh.service'
        'usr/lib/avahi/service-types.db'
        'usr/share/avahi/service-types')
source=("http://www.avahi.org/download/avahi-${pkgver}.tar.gz")
sha1sums=('7e05bd78572c9088b03b1207a0ad5aba38490684')

conflicts=('howl' 'mdnsresponder' 'avahi')
provides=('howl' 'mdnsresponder' 'avahi')

install=install

prepare() {
	cd "${srcdir}/avahi-${pkgver}"
	sed '/^Libs:/s:$: -ldbus-1:' -i avahi-client.pc.in
	sed 's:netdev:network:g' -i avahi-daemon/avahi-dbus.conf
	sed 's:/sbin/resolvconf:/usr/sbin/resolvconf:g' -i */*.action
}

build() {
	cd "${srcdir}/avahi-${pkgver}"
	export MOC_QT3=/usr/bin/moc-qt3
	export MOC_QT4=/usr/bin/moc-qt4
	export PYTHON=/usr/bin/python2

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--disable-static \
		--disable-monodoc \
		--enable-compat-libdns_sd \
		--enable-compat-howl \
		--with-distro=gentoo \
		--with-avahi-priv-access-group=network \
		--with-autoipd-user=avahi \
		--with-autoipd-group=avahi \
		--disable-gtk3 \

	make
}

package() {
	cd "${srcdir}/avahi-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# howl and mdnsresponder compatability
	cd "${pkgdir}"/usr/include; ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h; ln -s avahi-compat-howl howl
	cd "${pkgdir}"/usr/lib/pkgconfig; ln -s avahi-compat-howl.pc howl.pc

	_fix_init "${pkgdir}/etc/init.d/avahi-daemon"
	_fix_init "${pkgdir}/etc/init.d/avahi-dnsconfd"
}

_fix_init(){
  sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|opts|extra_started_commands|' -i $1
}
