# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

_name="dbus"
_cat="sys-apps"
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/${_cat}/${_name}/files"

pkgname=dbus-openrc
pkgver=1.6.14
pkgrel=1
pkgdesc="Freedesktop.org message bus system with openrc support"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
groups=('openrc-desktop')
depends=('expat' 'coreutils' 'filesystem' 'openrc')
makedepends=('libx11' 'udev')
optdepends=('libx11: dbus-launch support')
provides=('dbus-core' 'dbus')
conflicts=('dbus')
#replaces=('dbus-core')
install=${_name}.install
options=(!libtool)
source=("http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz"
	"${_gentoo_uri}/dbus.initd"
        '30-dbus')

build() {
  cd dbus-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/dbus-1.0 \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-console-auth-dir=/run/console/ \
    --enable-inotify \
    --disable-dnotify \
    --disable-verbose-mode \
    --disable-static \
    --disable-tests  \
    --disable-asserts \
    --disable-systemd

  make
}

package(){
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/var/run"

  install -Dm755 ${srcdir}/30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"

  install -Dm755 "${srcdir}/dbus.initd" "${pkgdir}/etc/init.d/dbus"
  _fix_init "${pkgdir}/etc/init.d/dbus"
}

_fix_init(){
  sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|dbus.pid|dbus/pid|g' -e 's|/var/run|/run|g' -i $1
}
sha256sums=('5676011d5714682062c4cb0cdd3365c15025809bc26ddbec3e4edeea7867f56c'
            '98e37b8b6ed25004e48c5855d74c9361eea06d3fee13cefcc0ed10ccf452aa01'
            'a1c324ae758046f0c3ef46db02ab873d35adeaab9980c2e0ee293accb89d0d2c')
