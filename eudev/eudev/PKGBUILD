# Maintainer: artoo <flower_of_life@gmx.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=213
#_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"

pkgbase=eudev
pkgname=('eudev' 'eudev-openrc')
pkgver=1.7
pkgrel=3
arch=('i686' 'x86_64')
license=('GPL')
url="http://dev.gentoo.org/~blueness/eudev"
makedepends=('acl' 'dbus' 'hwids' 'kbd' 'kmod' 'pam'
			'gobject-introspection' 'gperf' 'gtk-doc' 'intltool')
options=(!makeflags !libtool)
source=("${url}/${pkgbase}-${pkgver}.tar.gz"
		'initcpio_hooks'
		'initcpio_install'
		#"${_gentoo_uri}/sys-fs/eudev/files/udev-postmount")
		'udev-postmount')
sha256sums=('7c4186904a87e553ec731458478e53a2bed97331e6ba215c4c480a8e2e6e1930'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1a70e48d5f5b476ed624f745b03988f2e8950e33ae864c6b1c4ef088882c8390'
            '0220e949b1f31832a205021c0a8615e77b27b73b7c9b16ef4a61bbd2774aeecb')

_pkgver() {
  date +%Y%m%d
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
		--with-modprobe=/usr/bin/modprobe \
		--with-html-dir=/usr/share/doc/${pkgbase}-${pkgver}/html \
		--enable-doc \
		--enable-gtk-doc \
		--enable-gudev \
		--enable-introspection \
		--enable-split-usr \
		--enable-rule-generator
	make
}

package_eudev() {
	pkgdesc="The userspace dev tools (udev) forked by Gentoo"
	depends=('glib2' 'hwids' 'kbd' 'kmod')
	optdepends=('eudev-openrc: eudev postmount script'
			'dbus-eudev: dbus without systemd'
			'upower-pm-utils-eudev: upower without systemd'
			'udisks2-eudev: udisks2 without systemd'
			'libpulse-eudev: libpulse without systemd'
			'cups-eudev: cups without systemd'
			'procps-ng-eudev: procps-ng without systemd')
	provides=("udev=${_udevver}"
			"systemd=${_udevver}"
			"libsystemd=${_udevver}"
			"systemd-tools=${_udevver}"
			'libudev.so'
			'libgudev-1.0.so')
	conflicts=('systemd'
			'libsystemd'
			'systemd-tools')
	backup=('etc/udev/udev.conf')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
		sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
				s#GROUP="tape"#GROUP="storage"#g;
				s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
	done

	install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
	install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
}

package_eudev-openrc(){
	pkgver=$(_pkgver)
	pkgrel=1
	pkgdesc="OpenRC eudev postmount script"
	arch=('any')
	groups=('eudev-base')
	depends=('eudev' 'openrc-core')
	conflicts=('openrc-arch-services-git'
			'initscripts'
			'systemd-sysvcompat'
			'openrc'
			'openrc-git')
	install=eudev.install

	install -Dm755 "${srcdir}/udev-postmount" "${pkgdir}/etc/init.d/udev-postmount"

	local _shebang='s|#!/sbin/runscript|#!/usr/bin/runscript|'
	sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/udev-postmount"
}
