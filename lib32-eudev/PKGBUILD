# Maintainer: artoo <flower_of_life@gmx.net>

_udevver=216
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"
_pkgname=eudev

pkgname=lib32-eudev
pkgdesc="The userspace dev tools (udev) forked by Gentoo (32-bit)"
pkgver=1.10
pkgrel=1
arch=('x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
license=('GPL')
depends=('lib32-glib2' 'eudev')
makedepends=('git' 'gcc-multilib' 'lib32-util-linux' 'gobject-introspection' 'gperf' 'python2')
provides=("lib32-udev=${_udevver}")
conflicts=('libgudev-1.0.so' 'libudev.so')
options=(!makeflags !libtool)
source=("${url}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('401eeb29147d3e24d87849a8006df62eadeb369d913484d59e9e35f3f73a20d1')

build() {

    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${_pkgname}-${pkgver}"

    ./configure \
	--prefix=/usr \
        --with-rootprefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib32 \
        --sbindir=/usr/bin \
        --with-modprobe=/usr/bin/modprobe \
        --enable-gudev \
        --enable-introspection \
	--disable-manpages
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    rm -rf "${pkgdir}"/etc
    rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
}
