# Maintainer: artoo <artoo@manjaro.org>

_udevver=220
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"
_pkgname=eudev

pkgname=lib32-eudev
pkgdesc="The userspace dev tools (udev) forked by Gentoo (32-bit)"
pkgver=3.1.1
pkgrel=1
arch=('x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
license=('GPL')
depends=('lib32-glib2' 'eudev')
makedepends=('git' 'gcc-multilib' 'lib32-util-linux' 'gobject-introspection' 'gperf' 'python2')
provides=("lib32-udev=${_udevver}")
conflicts=('libgudev-1.0.so' 'libudev.so')
options=(!makeflags !libtool)
source=("${url}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('d3a0c7f613d4917b89afd57498860a1c35b9c59abf9fea22a09c30eec25c32e4')

build() {

    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${_pkgname}-${pkgver}"

    ./configure \
	--prefix=/usr \
        --with-rootprefix=/usr \
        --sysconfdir=/etc \
        --libdir=/usr/lib32 \
        --sbindir=/usr/bin \
        --with-modprobe=/usr/bin/modprobe \
        --enable-gudev \
        --enable-introspection \
	--disable-manpages
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    #make DESTDIR="${pkgdir}" install
    make DESTDIR="${pkgdir}" -C src/shared uninstall
    make DESTDIR="${pkgdir}" -C src/libudev install
    make DESTDIR="${pkgdir}" -C src/gudev install
    make DESTDIR="${pkgdir}" -C src/shared uninstall

    #rm -rf "${pkgdir}"/etc
    #rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
}
