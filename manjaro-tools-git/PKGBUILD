# Maintainer: artoo <artoo@manjaro.org>

pkgname=manjaro-tools-git
pkgver=r954.d3b4806
_pkgver=0.9.7.2
pkgrel=1
pkgdesc='Development tools for Manjaro Linux'
arch=('any')
license=('GPL')
url='https://github.com/manjaro/manjaro-tools'
depends=('namcap' 'openssh' 'rsync'  'mkinitcpio-nfs-utils' 'nbd'
	'libisoburn' 'squashfs-tools' 'syslinux' 'haveged')
makedepends=('git')
optdepends=('qemu: quickly test isos' 'manjaro-iso-profiles: Manjaro Linux ISO-Image profiles')
provides=('devtools' 'arch-install-scripts' "manjaro-tools=$_pkgver" 'manjaroiso')
conflicts=('devtools' 'arch-install-scripts' "manjaro-tools=$_pkgver" 'manjaroiso')
backup=('etc/manjaro-tools/manjaro-tools.conf')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd ${srcdir}/manjaro-tools
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd ${srcdir}/manjaro-tools
	make SYSCONFDIR=/etc PREFIX=/usr
}

package() {
	cd ${srcdir}/manjaro-tools
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install
}
