# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <flower_of_life@gmx.net>
 
pkgbase=manjaro-tools-livecd
pkgname=('manjaro-livecd'
	'manjaro-livecd-openrc' 
	'manjaro-livecd-systemd'
	'manjaro-livecd-cli-installer')
pkgver=r37.496d129
pkgdesc='Manjaro Linux livecd'
pkgrel=1
arch=('any')
url="https://github.com/udeved/manjaro-tools-livecd"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd ${srcdir}/${pkgbase}
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_manjaro-livecd() {

	cd ${srcdir}/${pkgbase} #-${pkgver}
	install -dm 755 $pkgdir
	cp -a livecd/* $pkgdir
	chmod 750 $pkgdir/etc/sudoers.d
}

package_manjaro-livecd-cli-installer() {
	pkgdesc='Manjaro Linux livecd cli-installer'
	depends=('manjaro-livecd')
	
	cd ${srcdir}/${pkgbase} #-${pkgver}
	install -dm 755 $pkgdir/opt/livecd
	cp -a livecd-cli-installer/* $pkgdir/opt/livecd
}

package_manjaro-livecd-openrc() {
	pkgdesc='Manjaro Linux livecd openrc init scripts'
	depends=('openrc-core' 'manjaro-livecd')
	
	cd ${srcdir}/${pkgbase} #-${pkgver}
	install -dm 755 $pkgdir
	cp -a livecd-init-openrc/* $pkgdir
	
}

package_manjaro-livecd-systemd() {
	pkgdesc='Manjaro Linux livecd systemd units'
	depends=('systemd' 'manjaro-livecd')
	
	cd ${srcdir}/${pkgbase} #-${pkgver}
	install -dm 755 $pkgdir
	cp -a livecd-init-systemd/* $pkgdir
}
