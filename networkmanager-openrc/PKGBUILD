# Maintainer: artoo <flower_of_life@gmx.net>

_name=NetworkManager
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-misc/networkmanager/files"
_pppver=2.4.7

pkgname=networkmanager-openrc
pkgver=0.9.10.0
pkgrel=3
pkgdesc="Network Management daemon for consolekit and openrc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/${_name}/"
groups=('openrc' 'openrc-mobile')
depends=('libnm-glib' 'iproute2' 'libnl' 'polkit-consolekit' 'wpa_supplicant' 'dhcp-client' 'libsoup' 'libmm-glib' 'libnewt' 'libndp' 'libteam' 'udev' 'dbus-openrc' 'consolekit-openrc')
makedepends=('intltool' 'dhcpcd' 'dhclient' 'iptables' 'gobject-introspection' 'gtk-doc' 'git' "ppp=$_pppver"
             'modemmanager' 'dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'wpa_supplicant' 'dhcp-client' 'libsoup' 'libmm-glib' 'rp-pppoe' 'libnewt' 'libndp' 'libteam' 'vala')
optdepends=('dhclient: DHCPv6 support'
	    'dnsmasq: Connection sharing'
	    'bluez: Bluetooth support'
	    'openresolv: resolvconf support'
	    'ppp: Dialup connection support')
provides=("networkmanager=${pkgver}" "libnm-glib=${pkgver}")
conflicts=('networkmanager' 'libnm-glib')
backup=("etc/${_name}/${_name}.conf")
install=networkmanager.install
source=("http://ftp.gnome.org/pub/gnome/sources/${_name}/${pkgver:0:3}/${_name}-${pkgver}.tar.xz"
        'disable_set_hostname.patch'
        "${_gentoo_uri}/10-openrc-status-r4"
        "${_gentoo_uri}/conf.d.${_name}"
        "${_gentoo_uri}/init.d.${_name}"
        "${_gentoo_uri}/nm-system-settings.conf-ifnet")

prepare() {
  cd ${_name}-${pkgver}
  patch -Np1 -i ${srcdir}/disable_set_hostname.patch
}


build() {
  cd ${_name}-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --with-dhcpcd=/usr/bin/dhcpcd \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-iptables=/usr/bin/iptables \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-pppd=/usr/bin/pppd \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-pppoe=/usr/bin/pppoe \
    --with-kernel-firmware-dir=/usr/lib/firmware \
    --with-session-tracking=ck \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --enable-doc \
    --enable-ifnet

  make
}

package() {
  cd ${_name}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m644 "${srcdir}/nm-system-settings.conf-ifnet" "${pkgdir}/etc/${_name}/${_name}.conf"

  rm -r "${pkgdir}/var/run"

  install -Dm755 "${srcdir}/conf.d.${_name}" "${pkgdir}/etc/conf.d/networkmanager"
  install -Dm755 "${srcdir}/init.d.${_name}" "${pkgdir}/etc/init.d/networkmanager"

  install -Dm755 "${srcdir}/10-openrc-status-r4" "${pkgdir}/etc/${_name}/dispatcher.d/10-openrc-status"

  sed -e 's|@EPREFIX@/sbin|/bin|' -e 's|@EPREFIX@/usr/sbin|/usr/bin|' -e 's/NetworkManager/networkmanager/g' -e 's|#!/bin/sh|#!/usr/bin/sh|' -i "${pkgdir}/etc/${_name}/dispatcher.d/10-openrc-status"

  sed -e 's|#!/sbin/runscript|#!/usr/bin/openrc-run|' -e 's|/usr/sbin|/usr/bin|g' -i "${pkgdir}/etc/init.d/networkmanager"


#   install -d -m700 ${pkgdir}/etc/polkit-1/rules.d/
#   install -D -m644 ${srcdir}/01-org.freedesktop.NetworkManager.settings.modify.system.rules ${pkgdir}/etc/polkit-1/rules.d/
#   chown 102 ${pkgdir}/etc/polkit-1/rules.d

}
