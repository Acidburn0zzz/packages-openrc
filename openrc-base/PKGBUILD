# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: williamh <williamh@gentoo.org>

# set vars: 0=off; 1=on
_use_netifrc=1
_use_udev=1
_use_kmod=1
_use_scripts=1

_cat="sys-apps"
_name="openrc"
_gentoo_uri=("http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86")
_src_uri="http://dev.gentoo.org/~williamh/dist"

_udev="udev-init-scripts"
_uver=26

_net="netifrc"
_nver=0.1

if (( ${_use_netifrc} ));then
	_net_confd='etc/conf.d/net'
else
	_net_confd='etc/conf.d/network'
fi

pkgname=${_name}-base
pkgver=0.12.4
pkgrel=7
pkgdesc="Gentoo's universal init system, udev enabled."
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('GPL2')
groups=('openrc' 'openrc-base')
depends=('inetutils' 'net-tools' 'pkg-config' 'psmisc' 'sysvinit' 'udev')
optdepends=('dhcpcd-openrc: dhcpcd initscript')
conflicts=('initscripts' 'systemd-sysvcompat' 'openrc' 'openrc-git')
backup=('etc/rc.conf' 'etc/conf.d/consolefont' 'etc/conf.d/keymaps'
		'etc/conf.d/hostname' 'etc/conf.d/modules' "${_net_confd}")
install=${_name}.install
source=("${_src_uri}/${_name}-${pkgver}.tar.bz2"
		"${_src_uri}/${_udev}-${_uver}.tar.bz2"
		"${_src_uri}/${_net}-${_nver}.tar.bz2"
		"${_gentoo_uri}/${_cat}/${_name}/files/${_name}.logrotate"
		"${_gentoo_uri}/${_cat}/kmod/files/kmod-static-nodes"
		"${_gentoo_uri}/${_cat}/sysvinit/files/reboot.sh"
		"${_gentoo_uri}/${_cat}/sysvinit/files/shutdown.sh")

sha256sums=('c4edda2fff4b613f50b9cc265bb457a9ab0170fbc1fe7c26eccd4a5d63b2625c'
			'eff4cd581cb5d57a1fa09772f9f82a37d9a33a3828f0b7e988278a974864d203'
			'65eb507bda9ed51fdac2dcf70de3428229758257039ea4ce8360edb0903e39e9'
			'0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
			'078b07581e63b5113cef9e85da7d5b53e1fbad91d793f7fa8b7188b364689b0e'
			'967fa572014ac6dd69f5e7e24d5250abad9c20c644563b927b295778608cafef'
			'2cc84a5194a949f16a82c6215459f4bf6d8156e50d8d9130d310f085bb208d4c')

_shebang='s|#!/sbin/runscript|#!/usr/bin/runscript|'

_base_args=(SYSCONFDIR=/etc)
_base_args+=(BRANDING='Arch Linux')
_base_args+=(PREFIX=/usr)
_base_args+=(SBINDIR=/usr/bin)

_rc_args=( "${_base_args[@]}" )
_rc_args+=(LIBEXECDIR=/usr/lib/rc)
_rc_args+=(MKSELINUX=no)
_rc_args+=(MKPAM=pam)
_rc_args+=(MKTERMCAP=ncurses)

if (( ${_use_netifrc} ));then
	_net_args=( "${_base_args[@]}" )
	_net_args+=(LIBEXECDIR=/usr/lib/${_net})
	_rc_args+=(MKNET=oldnet)
fi

prepare(){
	cd "${srcdir}/${_name}-${pkgver}"
	sed -e 's|/sbin/|/usr/bin/|g' -i "support/sysvinit/inittab"
}

build(){
	if (( ${_use_netifrc} ));then
		cd "${srcdir}/${_net}-${_nver}"
		make "${_net_args[@]}"
	fi
	cd "${srcdir}/${_name}-${pkgver}"
	make "${_rc_args[@]}"
}

package() {
	cd "${srcdir}/${_name}-${pkgver}"
	make DESTDIR="${pkgdir}" "${_rc_args[@]}" install
	# inittab
	install -m 644 "${srcdir}/${_name}-${pkgver}/support/sysvinit/inittab" "${pkgdir}/etc/inittab"
	# logrotate
	install -Dm 0644 "${srcdir}/${_name}.logrotate" "${pkgdir}/etc/logrotate.d/${_name}"
	# enable unicode & logger
	sed -e 's/#unicode="NO"/unicode="YES"/' -i "${pkgdir}/etc/rc.conf"
	sed -e 's/#rc_logger="YES"/rc_logger="YES"/' -i "${pkgdir}/etc/rc.conf"
	# oldnet
	if (( ${_use_netifrc} ));then
		_install_netifrc
	fi
	# shutdown & reboot scripts for certain console setups
	if (( ${_use_scripts} ));then
		_install_scripts
	fi
	# kmod-static-nodes
	if (( ${_use_kmod} ));then
		_install_kmod
	fi
	# udev
	if (( ${_use_udev} ));then
		_install_udev
	fi
}

_install_netifrc(){
	cd "${srcdir}/${_net}-${_nver}"
	make DESTDIR="${pkgdir}" "${_net_args[@]}" install
	install -Dm 644 "${srcdir}/${_net}-${_nver}/doc/net.example" "${pkgdir}/etc/conf.d/net"
	# create runlevel
 	ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo"
}

_install_scripts(){
	cd "${srcdir}/${_name}-${pkgver}"
	install -Dm755 ${srcdir}/reboot.sh ${pkgdir}/etc/init.d/reboot.sh
	install -Dm755 ${srcdir}/shutdown.sh ${pkgdir}/etc/init.d/shutdown.sh
	local _binpath='s|/sbin/|/usr/bin/|g'
	sed -e "${_binpath}" -i ${pkgdir}/etc/init.d/reboot.sh
	sed -e "${_binpath}" -i ${pkgdir}/etc/init.d/shutdown.sh
}

_install_kmod(){
	cd "${srcdir}/${_name}-${pkgver}"
	install -Dm755 "${srcdir}/kmod-static-nodes" "${pkgdir}/etc/init.d/kmod-static-nodes"
	# fix shebang
	sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/kmod-static-nodes"
	# create runlevel
	ln -sf "/etc/init.d/kmod-static-nodes" "${pkgdir}/etc/runlevels/sysinit/kmod-static-nodes"
}

_install_udev(){
	cd "${srcdir}/${_udev}-${_uver}"
	make DESTDIR="${pkgdir}" install
	# fix shebang & path to udevd
	local _binpath='s|/sbin/udevd|/usr/bin/udevd|g'
	sed -e "${_shebang}" -e "${_binpath}" -i "${pkgdir}/etc/init.d/udev"
	sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/udev-mount"
	# create runlevels
	cd "${srcdir}/${_name}-${pkgver}"
	ln -sf "/etc/init.d/udev" "${pkgdir}/etc/runlevels/sysinit/udev"
	ln -sf "/etc/init.d/udev-mount" "${pkgdir}/etc/runlevels/sysinit/udev-mount"
}