# Maintainer: udeved <udeved@openrc4arch.site40.net>

_pgs_ver=2.5

pkgbase=openrc-devel
pkgname=openrc-devel
true && pkgname=('git-openrc' 'mysql-openrc'
		'postgresql-openrc' 'subversion-openrc')
pkgver=20131016
pkgrel=1
pkgdesc="OpenRC init scripts"
arch=('any')
url="http://openrc4arch.site40.net"
license=('GPL2')
groups=('openrc-devel')
makedepends=('openrc-base' 'git' 'mysql' 'postgresql' 'subversion')
conflicts=('openrc-arch-services-git')

_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"

source=("${_gentoo_uri}/dev-vcs/git/files/git-daemon.confd"
	"${_gentoo_uri}/dev-vcs/git/files/git-daemon.initd"
	"${_gentoo_uri}/dev-db/mysql-init-scripts/files/mysql-5.1.53-conf.d"
	"${_gentoo_uri}/dev-db/mysql-init-scripts/files/mysql-5.1.67-init.d"
	"http://dev.gentoo.org/~titanofold/postgresql-initscript-${_pgs_ver}.tbz2"
	"${_gentoo_uri}/dev-vcs/subversion/files/svnserve.confd"
	"${_gentoo_uri}/dev-vcs/subversion/files/svnserve.initd2")


pkgver() {
  date +%Y%m%d
}

_shebang='s|#!/sbin/runscript|#!/usr/bin/runscript|'
_runpath='s|/var/run|/run|g'
_binpath='s|/usr/sbin|/usr/bin|g'

package_git-openrc() {
	true
	pkgdesc="OpenRC git-daemon init script"
	depends=('openrc-base' 'git')
	install=git.install

	install -Dm755 "${srcdir}/git-daemon.confd" "${pkgdir}/etc/conf.d/git-daemon"
	install -Dm755 "${srcdir}/git-daemon.initd" "${pkgdir}/etc/init.d/git-daemon"

	local _p1='s|/var/git|/srv/git|'
	sed -e "${_p1}" -i "${pkgdir}/etc/conf.d/git-daemon"
	sed -e "${_shebang}" -e "${_binpath}" -e "${_runpath}" -i "${pkgdir}/etc/init.d/git-daemon"
}

package_mysql-openrc() {
	true
	pkgdesc="OpenRC mysql init script"
	depends=('openrc-base' 'mysql')
	optdepends=('bind-openrc')
	install=mysql.install

	install -Dm755 "${srcdir}/mysql-5.1.53-conf.d" "${pkgdir}/etc/conf.d/mysql"
	install -Dm755 "${srcdir}/mysql-5.1.67-init.d" "${pkgdir}/etc/init.d/mysql"

	local _p1='s|/sbin/mysqld|/usr/bin/mysqld|g'
	sed -e "${_shebang}" -e "${_p1}" -i "${pkgdir}/etc/init.d/mysql"
}

package_postgresql-openrc() {
	true
	pkgdesc="OpenRC postgresql init script"
	depends=('openrc-base' 'postgresql')
	install=postgresql.install

	install -Dm755 "${srcdir}/postgresql.confd" "${pkgdir}/etc/conf.d/postgresql"
	install -Dm755 "${srcdir}/postgresql.init" "${pkgdir}/etc/init.d/postgresql"

	local _p1='s|/var/lib/postgresql/@SLOT@/data|/var/lib/postgres/data|' _p2='s|/etc/postgresql-@SLOT@/|/var/lib/postgres/data|'
	sed -e "${_p1}" -e "${_p2}" -i "${pkgdir}/etc/conf.d/postgresql"

	_p1='s|/usr/lib/postgresql-@SLOT@/bin/pg_ctl|/usr/bin/pg_ctl|'
	_p2='s|/usr/lib/postgresql-@SLOT@/bin/postgres|/usr/bin/postgres|'
	sed -e "${_shebang}" -e "${_p1}" -e "${_p2}" -i "${pkgdir}/etc/init.d/postgresql"
}

package_subversion-openrc() {
	true
	pkgdesc="OpenRC svnserve init script"
	depends=('openrc-base' 'subversion')
	install=subversion.install

	install -Dm755 "${srcdir}/svnserve.confd" "${pkgdir}/etc/conf.d/svnserve-rc"
	install -Dm755 "${srcdir}/svnserve.initd2" "${pkgdir}/etc/init.d/svnserve-rc"

	local _p1='s|/var/svn|/srv/svn|g'
	sed -e "${_p1}" -i ${pkgdir}/etc/conf.d/svnserve-rc
	sed -e "${_shebang}" -e "${_binpath}" -e "${_runpath}" -e "${_p1}" -i "${pkgdir}/etc/init.d/svnserve-rc"
}
