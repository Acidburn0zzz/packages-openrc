# $Id: PKGBUILD 172792 2012-12-06 01:48:03Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
_pn=openrc-plugins
_rn=dbus
_pv=0.7

pkgname=dbus-openrc
pkgver=1.6.8
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
# do not depend on systemd to avoid circular dep,
# dep on shadow for install scriptlet FS#29341
depends=('expat' 'coreutils' 'filesystem' 'shadow')
makedepends=('libx11')
optdepends=('libx11: dbus-launch support')
provides=('dbus-core' 'dbus')
conflicts=('dbus-core' 'dbus')
#replaces=('dbus-core' 'dbus')
options=(!libtool)
install=dbus.install
source=("http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz" #{,.asc}
		"http://openrc4arch.site40.net/pool/archlinux/$_pn-$_pv.tar.xz"
        '30-dbus')

build() {
  cd dbus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --disable-systemd

  make
}

package(){
	cd dbus-$pkgver
	make DESTDIR="$pkgdir" install

	rm -rf "$pkgdir/var/run"
	
	install -Dm755 "$srcdir/$_pn/$_rn/init.d/dbus.initd" "$pkgdir/etc/init.d/dbus"

	install -Dm755 "$srcdir/30-dbus" "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus"

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}
sha256sums=('fc1370ef38abeeb13f55c905ec002e60705fb0bfde3b8d21c8d6eb8056c11bac'
            '0f1b2dbd2dd81c6ae53e3e118b113c7905a50eeb3ac373ff2956baeeb423d3bc'
            '24785097abec61b8f2b8f9532e83e3f49855f505555d1a0cbbab99beb254460c')
