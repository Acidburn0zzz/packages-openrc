# $Id: PKGBUILD 177641 2013-02-08 08:55:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace-consolekit
_pkgname=kde-workspace
pkgver=4.10.5
pkgrel=1
pkgdesc="Provides the consolkit enabled interface and basic tools for the KDE workspace"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('kdepim-runtime' 'lm_sensors' 'libraw1394' 'libqalculate'
         'qimageblitz' 'polkit-kde' 'xorg-xprop' 'libxdamage'
         'libxklavier' 'xorg-xsetroot' 'libxcomposite' 'libxinerama'
         'xorg-xrdb' 'libgles' 'libegl' 'libxres' 'xorg-xrandr'
         'xorg-xmessage' 'libusb-compat' 'kde-base-artwork'
         'xcb-util-renderutil' 'xcb-util-image' 'ttf-font')
makedepends=('cmake' 'automoc4' 'boost' 'kdebindings-python2' 'networkmanager' 'mesa')
optdepends=('kde-wallpapers: wallpapers for KDE Plasma Workspaces'
            'appmenu-qt: menu applications over dbus')
provides=('kdebase-workspace')
conflicts=('kdebase-workspace')
install="kdebase-workspace.install"
backup=('usr/share/config/kdm/kdmrc')
source=("http://download.kde.org/stable/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz"
        'kde.pam' 'kde-np.pam' 'kscreensaver.pam' 'kdm.service' 'kdm.logrotate'
        'etc-scripts.patch' 'terminate-server.patch' 'kdm-xinitrd.patch')

build() {
        cd ${_pkgname}-${pkgver}

        # reads the shell scripts in /etc/kde/
        patch -p0 -i "${srcdir}"/etc-scripts.patch
        # FS#26120
        patch -p1 -i "${srcdir}"/kdm-xinitrd.patch

        # KDEBUG#202629
        patch -p0 -i "${srcdir}"/terminate-server.patch

        cd ../

        mkdir build
        cd build
        cmake ../${_pkgname}-${pkgver} \
          -DCMAKE_BUILD_TYPE=Release \
          -DKDE4_BUILD_TESTS=OFF \
          -DCMAKE_SKIP_RPATH=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DWITH_Xmms=OFF \
          -DWITH_Googlegadgets=OFF \
          -DWITH_libgps=OFF \
          -DPYTHON_EXECUTABLE=/usr/bin/python2 \
          -DWITH_CkConnector=ON
        make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	install -D -m644 "${srcdir}"/kde.pam "${pkgdir}"/etc/pam.d/kde
	install -D -m644 "${srcdir}"/kde-np.pam "${pkgdir}"/etc/pam.d/kde-np
	install -D -m644 "${srcdir}"/kscreensaver.pam "${pkgdir}"/etc/pam.d/kscreensaver

	install -d -m755 "${pkgdir}"/usr/share/xsessions/
	ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop \
      "${pkgdir}"/usr/share/xsessions/
	install -d -m755 "${pkgdir}"/etc/kde/{env,shutdown}

	install -d -g 135 -o 135 "${pkgdir}"/var/lib/kdm
    install -D -m644 "${srcdir}"/kdm.service \
      "${pkgdir}"/usr/lib/systemd/system/kdm.service
    install -Dm644 "${srcdir}"/kdm.logrotate "${pkgdir}"/etc/logrotate.d/kdm
}

sha256sums=('a346420258f96475b47a7e3a78945fc38c10d4732b01d8fc371d11dcad56e875'
            '812111f166fdf9ab7ef9400c4414cbe26fac5210f2292399d4446fe6e89eea54'
            '3ca72a5f34d29747d12b9b41888d0e369956ba383478850c578e3aa109444214'
            '0a985e3b699578de3e22b25ee72abe4e24ca8730e4afb37dfeed367823bdb0d5'
            'b932b7761651df48746b7e5e178fd4c7d322e9f6c41dcf2ec2c51f91a6440d85'
            '6fdf115bd6b6cf15dddbe3e0eab00fb62a296d5292759f1a09dcb865a357c220'
            'a4c05882dd3a340960dec495cb2fd19cf2523d14a7add5b77af0f98cdff4c394'
            'e5329c87f14ce04f80291f1f7a2be47994f3d54425b572c308b38e2718390959'
            'df7fe57d73c5a017fed698976b00e6488b8c2f555ff9411e55c015cbcef3312b')
