# Maintainer: udeved <udeved@openrc4arch.site40.net>

_name="dhcp"
_cat="net-misc"
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/${_cat}/${_name}/files"

pkgname=${_name}-openrc
pkgver=20130821
pkgrel=1
pkgdesc="OpenRC ${_name} server and dhcrelay init script"
arch=('any')
url="http://openrc4arch.site40.net"
license=('GPL2')
depends=('openrc' "${_name}")
optdepends=('openldap-openrc' 'bind-openrc')
groups=('openrc-net')
install=${_name}.install
source=("${_gentoo_uri}/dhcpd.conf2"
		"${_gentoo_uri}/dhcpd.init5"
		"${_gentoo_uri}/dhcrelay.conf"
		"${_gentoo_uri}/dhcrelay6.conf"
		"${_gentoo_uri}/dhcrelay.init3")
sha256sums=('e8a413e9102948b336f60041fc3cade33125faf56d8319ee65d9f3c63199a8e7'
            '049a231908dc2c5f82446712d71d5e75d430bd043b53eead1529f2d81e1595ab'
            '409b895ed489e75c1a08f72961b45d641b93fd9357f528abc78b1fd036b7f730'
            '099f668e1ad42ed9446b15675032a1186715d1fe9e4a1b24dfb787e68495d2b6'
            '204aaf81d51dc119cee1d1ff3ae3dd50af666af2f8b2a363fd1bf442d4dcee00')

package() {
	install -Dm755 "$srcdir/dhcpd.conf2" "$pkgdir/etc/conf.d/dhcpd"
	install -Dm755 "$srcdir/dhcpd.init5" "$pkgdir/etc/init.d/dhcpd"
	
	install -Dm755 "$srcdir/dhcrelay.conf" "$pkgdir/etc/conf.d/dhcrelay"
	install -Dm755 "$srcdir/dhcrelay.init3" "$pkgdir/etc/init.d/dhcrelay"
	
	install -Dm755 "$srcdir/dhcrelay6.conf" "$pkgdir/etc/conf.d/dhcrelay6"
	install -Dm755 "$srcdir/dhcrelay.init3" "$pkgdir/etc/init.d/dhcrelay6"
	
	_fix_init "$pkgdir/etc/init.d/dhcpd"
	_fix_init "$pkgdir/etc/init.d/dhcrelay"
	_fix_init "$pkgdir/etc/init.d/dhcrelay6"
	_fix_init "$pkgdir/etc/conf.d/dhcpd"
}

_fix_init(){

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -e 's|/etc/dhcp|/etc|g' -e 's|#@slapd@|ldap slapd|' -i $1
	
}

