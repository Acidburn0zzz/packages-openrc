# Maintainer: udeved <udeved@openrc4arch.site40.net>

_name="ntp"
_cat="net-misc"
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/${_cat}/${_name}/files"

pkgname=${_name}-openrc
pkgver=20130821
pkgrel=1
pkgdesc="OpenRC ${_name} init script"
arch=('any')
url="http://openrc4arch.site40.net"
license=('GPL2')
groups=('openrc-desktop')
depends=('openrc' "${_name}")
optdepends=('bind-openrc')
provides=('openrc-timed')
conflicts=('openrc-timed')
install=${_name}.install
source=("${_gentoo_uri}/ntpd.confd"
		"${_gentoo_uri}/ntpd.rc"
		"${_gentoo_uri}/ntp-client.confd"
		"${_gentoo_uri}/ntp-client.rc")
sha256sums=('a3bc17ee0f7d41102d6c33e4281e22d312a42cc2ffd8848b08e9cd78d63f2d05'
            'f7c58e8f8e91ee0c1b947e9684b0a9e9e27220e0c97a8b06ee4e3bc5578a88b2'
            'e8d5883461f4297c052e3b39814b663965ed3a756c4234675f4a9b62349eca13'
            'bdd70612cf0654460aa4cfa59bcaaf6d636d240a3fead8b243c86e5cbe640ab9')

pkgver() {
  date +%Y%m%d
}

package() {
	install -Dm755 "$srcdir/ntpd.confd" "$pkgdir/etc/conf.d/ntpd"
	install -Dm755 "$srcdir/ntpd.rc" "$pkgdir/etc/init.d/ntpd"
	
	install -Dm755 "$srcdir/ntp-client.confd" "$pkgdir/etc/conf.d/ntp-client"
	install -Dm755 "$srcdir/ntp-client.rc" "$pkgdir/etc/init.d/ntp-client"
	
	_fix_init "$pkgdir/etc/init.d/ntpd"
	_fix_init "$pkgdir/etc/init.d/ntp-client"
}

_fix_init(){

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g'  -e 's|/var/run|/run|g' -i $1
	
}
