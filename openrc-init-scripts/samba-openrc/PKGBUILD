# Maintainer: udeved <udeved@openrc4arch.site40.net>

_name="samba"
_cat="net-fs"
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/${_cat}/${_name}/files/4.0"

pkgname=${_name}-openrc
pkgver=20130821
pkgrel=1
pkgdesc="OpenRC ${_name} init script"
arch=('any')
url="http://openrc4arch.site40.net"
license=('GPL2')
groups=('openrc-fs')
depends=('openrc' "${_name}")
install=${_name}.install
source=("${_gentoo_uri}/samba4.confd"
	"${_gentoo_uri}/samba4.initd-r1")
sha256sums=('53755809cc27fdbaf98752cd3da45b5c2bda62dd46fcc37570c6e237f7954ffa'
            '4d00243dbd85ee81e2e989f1ac8035c1c1b9379133584291b959bb7bc45fe780')

pkgver() {
  date +%Y%m%d
}

package() {
  install -Dm755 "${srcdir}/samba4.confd" "${pkgdir}/etc/conf.d/samba-rc"
  install -Dm755 "${srcdir}/samba4.initd-r1" "${pkgdir}/etc/init.d/samba-rc"
  _fix_init "${pkgdir}/etc/conf.d/samba-rc"
  _fix_init "${pkgdir}/etc/init.d/samba-rc"
}

_fix_init(){
  sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -e 's|/var/run|/run|g' -e 's|SVCNAME##samba|SVCNAME##samba-rc|g' -e 's|"samba"|"samba-rc"|g' -i $1
}
