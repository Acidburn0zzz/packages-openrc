# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=200

pkgname='eudev-git'
pkgver=20130414
pkgrel=1
pkgdesc="The userspace dev tools (udev) forked by Gentoo"
arch=('i686' 'x86_64')
url="https://github.com/gentoo/eudev"
license=('GPL')
#groups=('base')
depends=('util-linux' 'glib2' 'kmod' 'hwids' 'bash' 'acl')
makedepends=('git' 'gobject-introspection' 'gperf' 'libxslt' 'docbook-xsl')
optdepends=('cryptsetup: required for encrypted block devices'
			'quota-tools: kernel-level quota management'
			'udev-init-scripts: OpenRC udev init scripts')
provides=("udev=${_udevver}" "systemd=${_udevver}" "libsystemd=${_udevver}" "systemd-tools=${_udevver}")
conflicts=('udev' 'systemd' 'libsystemd' 'systemd-tools')
#replaces=('udev' 'systemd' 'libsystemd' 'systemd-tools')
backup=('etc/udev/udev.conf' 'etc/udev/rules.d/80-net-name-slot.rules')
options=('!libtool')
install=${pkgname}.install
source=('initcpio_hooks' 'initcpio_install' '80-net-name-slot.rules')

_gitroot=('git://github.com/gentoo/eudev.git')
_gitname=('eudev')
         
build() {

	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."
	
	cd "${srcdir}/${_gitname}"
	
	./autogen.sh
	./configure  \
			--prefix=/usr \
			--sysconfdir=/etc \
			--libdir=/usr/lib \
			--localstatedir=/var \
			--enable-static \
			--enable-libkmod \
			--enable-modules \
			--enable-keymap \
			--enable-gudev \
			--enable-split-usr \
			--enable-introspection \
			--with-rootprefix=/usr \
			--with-firmware-path="/usr/lib/firmware/updates:/usr/lib/firmware"

  msg "Compiling..."
  make
}

package() {
	cd "${srcdir}/${_gitname}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
	install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"

	for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
		sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
	done

	rm -f "${pkgdir}/usr/lib/udev/rules.d/80-net-name-slot.rules"
	install -Dm644 "${srcdir}/80-net-name-slot.rules" "${pkgdir}/etc/udev/rules.d/80-net-name-slot.rules"
  
	install -d "$pkgdir/sbin"
	ln -s /usr/bin/udevadm "$pkgdir/sbin/udevadm"
	ln -s /usr/sbin/udevd "$pkgdir/sbin/udevd"
}

sha256sums=('892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1f39e84acf7a4af51207fa41956ff8603c48a711723ca3e5693ec079a2aff59a'
            '90328b323ea8c51b8091f402e8937a1a2e85bb8f7e8cb61a716121982492720e')
