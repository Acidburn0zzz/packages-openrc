# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

pkgname='eudev-git'
pkgdesc="The userspace dev tools (udev) forked by Gentoo"
pkgver=20130405
pkgrel=1
_udevver=200
provides=("udev=${_udevver}" "systemd=${_udevver}" "libsystemd=${_udevver}" "systemd-tools=${_udevver}")
replaces=('udev' 'systemd' 'libsystemd' 'systemd-tools')
conflicts=('udev' 'systemd' 'libsystemd' 'systemd-tools')
#groups=('base')
arch=('i686' 'x86_64')
url="https://github.com/gentoo/eudev"
license=('GPL')
depends=('util-linux' 'glib2' 'kmod' 'hwids' 'bash' 'acl')
optdepends=('udev-openrc')
makedepends=('git' 'gobject-introspection' 'gperf' 'libxslt' 'docbook-xsl')
options=(!makeflags !libtool)
install="${pkgname}.install"
backup=('etc/udev/udev.conf' 'etc/udev/rules.d/80-net-name-slot.rules')
source=('initcpio_hooks'
        'initcpio_install'
	'80-net-name-slot.rules')
options=(!makeflags !libtool)

_gitroot=('git://github.com/gentoo/eudev.git')
_gitname=('eudev')
         
build() {
  if [ -d "${_gitname}" ] ; then
    cd "${srcdir}/${_gitname}"
    msg2 "Updating local tree..."
    git pull origin master
  else
    cd "${srcdir}"
    msg2 "Cloning initial copy of ${_gitname}..."
    git clone --depth=1 "${_gitroot}" "${_gitname}"
    cd "${srcdir}/${_gitname}"
  fi
  
  if [ -f "Makefile" ];then
    msg2 "Cleaning up..."
    make clean
  fi
  
  msg2 "Configuring sources..."
  ./autogen.sh
  ./configure  \
			--prefix=/usr \
			--sysconfdir=/etc \
			--libdir=/usr/lib \
			--enable-libkmod \
			--enable-modules \
			--enable-keymap \
			--enable-gudev \
			--enable-introspection \
			--with-rootprefix=/usr \
			--with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  msg2 "Compiling..."
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  # install the mkinitpcio hook
  install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  # Make new interface naming policy disabled by default
  rm -f "${pkgdir}/usr/lib/udev/rules.d/80-net-name-slot.rules"
  install -Dm644 "${srcdir}/80-net-name-slot.rules" "${pkgdir}/etc/udev/rules.d/80-net-name-slot.rules"
  
   install -d "$pkgdir/sbin"
   ln -s /usr/bin/udevadm "$pkgdir/sbin/udevadm"

}

sha256sums=('892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1f39e84acf7a4af51207fa41956ff8603c48a711723ca3e5693ec079a2aff59a'
            '90328b323ea8c51b8091f402e8937a1a2e85bb8f7e8cb61a716121982492720e')
