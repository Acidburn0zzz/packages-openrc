# $Id: PKGBUILD 79754 2012-11-11 21:14:11Z spupykin $
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Paul Mattal <paul.archlinux.org>

_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sys-process/dcron/files"

pkgname=dcron-openrc
pkgver=4.5
pkgrel=1
pkgdesc="dillon's lightweight cron daemon for openrc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jimpryor.net/linux/dcron.html"
backup=('var/spool/cron/root' 'etc/conf.d/crond')
depends=('bash' 'openrc')
provides=('cron' 'openrc-cron')
conflicts=('cron' 'openrc-cron')
optdepends=('smtp-server: sending cron job output via email')
install=$_rn.install
source=("http://www.jimpryor.net/linux/releases/dcron-${pkgver}.tar.gz"
		"${_gentoo_uri}/dcron.init-4.5"
		"${_gentoo_uri}/dcron.confd-4.4")


build() {
	cd "$srcdir/dcron-${pkgver}"

	# by default, any member of group "users" can edit their own crontab
	make \
		PREFIX=/usr \
		CRONTAB_GROUP=users \
		CRONTABS=/var/spool/cron \
		CRONSTAMPS=/var/spool/cronstamps
}

package() {
	cd "$srcdir/dcron-${pkgver}"
	make DESTDIR="$pkgdir" install

	# install standard configuration and scripts
	install -d -m755 "$pkgdir/etc/cron."{hourly,daily,weekly,monthly} 

	install -D -m755 extra/run-cron "$pkgdir/usr/sbin/run-cron"
	install -D -m0600 extra/root.crontab "$pkgdir/var/spool/cron/root" 
	install -D -m755 extra/crond.rc "$pkgdir/etc/rc.d/crond"
	install -D -m0644 extra/crond.conf "$pkgdir/etc/conf.d/crond"
	install -D -m644 extra/crontab.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/crontab.vim"
	sed -i -e 's=/var/spool/cron/cronstamps=/var/spool/cronstamps=' extra/prune-cronstamps
	install -D -m755 extra/prune-cronstamps "$pkgdir/etc/cron.d/prune-cronstamps"
	
	
	install -Dm755 "$srcdir/dcron.confd-4.4" "$pkgdir/etc/conf.d/dcron"
	install -Dm755 "$srcdir/dcron.init-4.5" "$pkgdir/etc/init.d/dcron"
	
}
sha256sums=('9e50edb6f5bd8153b16bad05087d985e5153ce45cc01ae77e7f842213fb4a824'
            'f48b26fc1eacff7298c2f3e2e2a3144280429785c6a067fbe66c71976a9fea1c'
            '1e273164865bb8f05d97ef1479c285b86f4104951ac294af883eb65ce100ccd0')
