# $Id: PKGBUILD 158661 2012-05-05 22:14:03Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

_pn=openrc-plugins
_rn=sysklogd
_pv=0.9.1

pkgname=$_rn-openrc
pkgver=1.5
pkgrel=1
pkgdesc="System and kernel log daemons"
arch=('i686' 'x86_64')
url="http://www.infodrom.org/projects/sysklogd/"
license=('GPL' 'BSD')
depends=('glibc' 'bash' 'openrc')
provides=('logger')
backup=('etc/syslog.conf' 'etc/logrotate.d/syslog')
source=("http://www.infodrom.org/projects/sysklogd/download/${_rn}-${pkgver}.tar.gz"
        'sysklogd-1.4.1-caen-owl-syslogd-bind.diff'
        'sysklogd-1.4.1-caen-owl-syslogd-drop-root.diff'
        'sysklogd-1.4.1-caen-owl-klogd-drop-root.diff'
        'sysklogd-1.5-syslog-func-collision.patch'
        "http://openrc4arch.site40.net/pool/archlinux/$_pn-$_pv.tar.xz"
        'syslog.conf' 'syslog.logrotate' 'LICENSE')

build() {
  cd "${srcdir}/${_rn}-${pkgver}"
# CAEN/OWL security patches
  patch -p1 -i ../sysklogd-1.4.1-caen-owl-syslogd-bind.diff
  patch -p1 -i ../sysklogd-1.4.1-caen-owl-syslogd-drop-root.diff 
  patch -p1 -i ../sysklogd-1.4.1-caen-owl-klogd-drop-root.diff

  patch -p1 -i ../sysklogd-1.5-syslog-func-collision.patch
  sed -i -e "s/-O3/${CFLAGS} -D_FILE_OFFSET_BITS=64 -D_LARGEFILE_SOURCE/" \
         -e "s/LDFLAGS= -s/LDFLAGS= ${LDFLAGS}/" Makefile
  sed -i 's/500 -s/755/' Makefile
  make
}

package() {
	cd "${srcdir}/${_rn}-${pkgver}"
	install -d "${pkgdir}/usr/sbin" "${pkgdir}"/usr/share/man/{man5,man8}
 
	make prefix="${pkgdir}" install
  
	install -D -m644 ../syslog.conf "${pkgdir}/etc/syslog.conf"
	install -D -m644 ../syslog.logrotate "${pkgdir}/etc/logrotate.d/syslog"

	install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${_rn}/LICENSE"
  
	install -Dm755 "$srcdir/$_pn/$_rn/conf.d/sysklogd.confd" "$pkgdir/etc/conf.d/$_rn"
	install -Dm755 "$srcdir/$_pn/$_rn/init.d/sysklogd.rc7" "$pkgdir/etc/init.d/$_rn"	
  
}
sha256sums=('6169b8e91d29288e90404f01462b69e7f2afb1161aa419826fe4736c7f9eb773'
            'db2f7a8cac6d21dbba72c38a08248b6aa21e32be5e5d1640c48074efe6b1a3ab'
            '0f08fecee20d69021dce3b9dc2b901c4509f9501f804f0fd88445e7751d9a16a'
            'c79edeb73f16abe0d54ce9ac582bb0796c07c2541963c742f4dd0b3c1b8d40bc'
            'e035765eb6d7f013d42d872dc0561deac52dccaf00fa26e21504891b3e0ae14a'
            '7f798793e431ab873f9677dc30560251d23aeb812da941f8c034ba6bddc3c0c7'
            'c46c311d3dbb75721198939fc2777d4f8dec82e0fc5fd0fc7adcd9e8f50e237f'
            '13456b99a76408a47d21a385c0208b141597f202e7b0f6de67b9122fa18dba7e'
            '5d15a27dc7c6183a61a0e44fa5559b6d53a9f1591767c2322dd797f717c5eb8d')
