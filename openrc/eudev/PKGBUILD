# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

pkgname='eudev'
pkgver=1.3
pkgrel=1
pkgdesc="The userspace dev tools (udev) forked by Gentoo with openrc support"
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
license=('GPL')
#groups=('openrc-base')
depends=('acl' 'bash' 'glib2' 'hwids' 'kmod' 'udev-init-scripts' 'util-linux')
makedepends=('docbook-xsl' 'gobject-introspection' 'gperf' 'libxslt')
_udevver=208
provides=("udev=${_udevver}" "systemd=${_udevver}" "libsystemd=${_udevver}" "systemd-tools=${_udevver}")
conflicts=('udev' 'systemd' 'libsystemd' 'systemd-tools')
backup=('etc/udev/udev.conf')
install=${pkgname}.install
options=(!makeflags !libtool)
source=("${url}/${pkgname}-${pkgver}.tar.gz"
	"http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sys-fs/eudev/files/udev-postmount"
        'initcpio_hooks'
        'initcpio_install')
sha256sums=('6f1f0b15971a53c7070a44c902c7c1303f952927aa6370dbdf4d84d8bc09fac2'
            '0220e949b1f31832a205021c0a8615e77b27b73b7c9b16ef4a61bbd2774aeecb'
            '892ce43218e0a458981bbce451252c8987dc398e60b8de288e7542b8f2409c13'
            '1a70e48d5f5b476ed624f745b03988f2e8950e33ae864c6b1c4ef088882c8390')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --with-rootprefix=/usr \
              --sysconfdir=/etc \
              --libdir=/usr/lib \
	      --sbindir=/usr/bin \
              --with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
              --with-modprobe=/usr/bin/modprobe \
              --enable-gudev \
              --enable-introspection \
              --enable-split-usr \
              --enable-rule-generator
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"

  install -Dm755 "${srcdir}/udev-postmount" "${pkgdir}/etc/init.d/udev-postmount"
  _fix_init "${pkgdir}/etc/init.d/udev-postmount"
}

_fix_init() {
    sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -i $1
}

