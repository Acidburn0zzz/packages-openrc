# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: williamh <williamh@gentoo.org>

pkgname=netifrc
pkgver=0.1
pkgrel=1
pkgdesc="Gentoo Network Interface Management Scripts"
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD')
depends=('openrc>=0.12')
source=("http://dev.gentoo.org/~williamh/dist/${pkgname}-${pkgver}.tar.bz2")

sha256sums=('65eb507bda9ed51fdac2dcf70de3428229758257039ea4ce8360edb0903e39e9')

prepare(){
	sed -i 's:0444:0644:' ${srcdir}/${pkgname}-${pkgver}/mk/sys.mk
	sed -i "/^DIR/s:/${pkgname}:/${pkgname}-${pkgver}:" ${srcdir}/${pkgname}-${pkgver}/doc/Makefile
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make \
		SYSCONFDIR='/etc' \
		OS='Linux' \
		BRANDING='Arch Linux' \
		PREFIX='/usr' \
		SBINDIR='/usr/bin' \
		LIBEXECDIR="/usr/lib/netifrc" \
		DESTDIR="${pkgdir}" \
		install
		
		install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/doc/net.example" "${pkgdir}/etc/conf.d/net"

}
