# $Id: PKGBUILD 179641 2013-03-08 23:40:46Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

_rn=NetworkManager

pkgname=networkmanager-openrc
pkgver=0.9.8.6
pkgrel=1
pkgdesc="Network Management daemon for consolekit and openrc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/$_rn/"
groups=('openrc-misc')
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'consolekit' 'udev' 'wpa_supplicant' 'dhcp-client'
         'libsoup' 'modemmanager')
makedepends=('intltool' 'dhcpcd' 'dhclient' 'iptables' 'gobject-introspection' 'gtk-doc' 'git')
optdepends=('dhclient: DHCPv6 support'
            'dnsmasq: Connection sharing'
            'bluez: Bluetooth support'
            'openresolv: resolvconf support'
            'ppp: Dialup connection support')
options=('!libtool')
provides=('networkmanager')
conflicts=('networkmanager')
options=('!libtool')
backup=("etc/$_rn/$_rn.conf")
install=networkmanager.install
_my_url="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-misc/networkmanager/files"
source=("http://ftp.gnome.org/pub/gnome/sources/$_rn/${pkgver:0:3}/$_rn-$pkgver.tar.xz"
        "$_my_url/10-openrc-status-r3"
        "$_my_url/conf.d.$_rn"
        "$_my_url/init.d.$_rn"
        "$_my_url/nm-system-settings.conf-ifnet"
        'disable_set_hostname.patch')

prepare() {
  cd $_rn-$pkgver
  patch -Np1 -i ../disable_set_hostname.patch
}

build() {
  cd $_rn-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --sbindir=/usr/bin \
    --without-netconfig \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --with-dhcpcd=/usr/bin/dhcpcd \
    --with-iptables=/usr/bin/iptables \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-session-tracking=ck \
    --disable-static \
    --enable-ifnet \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --enable-doc

  make
}

package() {
	cd $_rn-$pkgver
	make DESTDIR="$pkgdir" install

	install -m644 "$srcdir/nm-system-settings.conf-ifnet" "$pkgdir/etc/$_rn/$_rn.conf"

	rm -r "$pkgdir/var/run"

	install -Dm755 "$srcdir/conf.d.$_rn" "$pkgdir/etc/conf.d/networkmanager"

	install -Dm755 "$srcdir/init.d.$_rn" "$pkgdir/etc/init.d/networkmanager"

	install -Dm755 "$srcdir/10-openrc-status-r3" "$pkgdir/etc/$_rn/dispatcher.d/10-openrc-status"

	sed -e 's|@EPREFIX@/sbin|/bin|' -e 's|@EPREFIX@/usr/sbin|/usr/bin|' -e 's/NetworkManager/networkmanager/g' -e 's|#!/bin/sh|#!/usr/bin/sh|' -i "$pkgdir/etc/$_rn/dispatcher.d/10-openrc-status"

	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin/NetworkManager|/usr/bin/NetworkManager|g' -i "$pkgdir/etc/init.d/networkmanager"
}

