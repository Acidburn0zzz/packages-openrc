# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: williamh <williamh@gentoo.org>

_realname='openrc'

pkgname=openrc4arch
pkgver=0.11.8
pkgrel=10
pkgdesc="OpenRC universal init system."
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/$_realname/"
license=('BSD')
groups=('openrc4arch-base')
depends=('bash'
	'coreutils'
	'glibc'
	'inetutils'
	'ncurses'
	'net-tools'
	'pam'
	'psmisc'
	'sysvinit')
optdepends=('openrc4arch-virtual-syslog'
	'openrc4arch-virtual-cron')
conflicts=('initscripts'
	"$_realname"
	"$_realname-git"
	"$pkgname-git")
backup=('etc/rc.conf'
	'etc/inittab'
	'etc/conf.d/consolefont'
	'etc/conf.d/keymaps'
	'etc/conf.d/hwclock'
	'etc/conf.d/hostname'
	'etc/conf.d/modules')
source=("http://dev.gentoo.org/~williamh/dist/$_realname-$pkgver.tar.bz2"
	'consolefont.patch'
	'openrc.logrotate'
	'runlevel.patch')
md5sums=('d896371d533a22de4cf57c7371c4d6e8'
         'd21ffb2a2d7d5e511daca45d746c2675'
         'ede356beae529d1b16b769c9da70ad52'
         'd1a5e62f0668c787a83cf9fe392a39cd')

build() {
	cd "$srcdir/$_realname-$pkgver"
	msg "patching ..."
		# both issues fixed in git
		patch -p1 -i $srcdir/consolefont.patch
		patch -p1 -i $srcdir/runlevel.patch
	msg "patching done"
}

package() {
	cd "$srcdir/$_realname-$pkgver"

	make \
		SYSCONFDIR='/etc' \
		OS='Linux' \
		BRANDING='Arch Linux' \
		PREFIX='/usr' \
		LIBEXECDIR='/usr/lib/rc' \
		MKSELINUX='no' \
		MKPAM='pam' \
		MKTERMCAP='ncurses' \
		MKNET='oldnet' \
		DESTDIR="$pkgdir" \
		install

	install -m 644 "$srcdir/$_realname-$pkgver/support/sysvinit/inittab" "$pkgdir/etc/inittab"

	install -Dm 0644 "$srcdir/$_realname.logrotate" "$pkgdir/etc/logrotate.d/$_realname"

	install -m 644 "$srcdir/$_realname-$pkgver/conf.d/net" "$pkgdir/etc/conf.d/net"

	_create_symlink

	_set_rc_option '#unicode="NO"' 'unicode="YES"'

	_set_rc_option '#rc_logger="YES"' 'rc_logger="YES"'
}

_create_symlink() {
	mkdir "$pkgdir/sbin"
	ln -s "/usr/sbin/rc" "$pkgdir/sbin/rc"
}

_set_rc_option() {
	# $1: old option string
	# $2: new option string

	local file="$pkgdir/etc/rc.conf"
	local pattern1=$1 pattern2=$2
	if ! grep -Fxq "$pattern2" "$file"; then
		msg "set $pattern2"
		sed -i "s/$pattern1/$pattern2/g" "$file"
	else
		msg "$pattern2 already set"
	fi
}
