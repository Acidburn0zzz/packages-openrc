# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-consolekit
pkgver=0.111
pkgrel=1
pkgdesc="PolyciKit with ConsoleKit support for non-systemd systems"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'pam' 'expat' 'js185')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
provides=('polkit')
conflicts=('polkit')
options=('!libtool')
install=polkit.install
source=("http://www.freedesktop.org/software/polkit/releases/polkit-$pkgver.tar.gz"
		'polkit.pam')
sha256sums=('02ae544547211b687818c97bcbf19bf6b8b5be7fda93000525a8765c7bed1ea1'
            'a35af3210bff1a8f2c04add47ed131a17df9eafbbc6f1671f97021be3ab1ff34')

build() {
  cd "$srcdir/polkit-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/polkit-1 \
    --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-libsystemd-login=no \
    --enable-gtk-doc
  make
}

package() {
  cd "$srcdir/polkit-$pkgver"
  make DESTDIR="$pkgdir" install

  chown 102 "$pkgdir/etc/polkit-1/rules.d"
  chown 102 "$pkgdir/usr/share/polkit-1/rules.d"

  install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"
}

