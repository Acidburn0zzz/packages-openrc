# Maintainer: artoo <artoo@manjaro.org>
# Contributor: kozec <kozec at kozec.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-consolekit
pkgver=0.112
pkgrel=5
pkgdesc="PolyciKit with ConsoleKit support for non-systemd systems"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'pam' 'expat' 'js17')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'git')
provides=("polkit=$pkgver")
conflicts=('polkit')
options=('!libtool')
install=polkit.install
source=("git://anongit.freedesktop.org/polkit#commit=fb5076b7c05d01a532d593a4079a29cf2d63a228"
		'polkit.pam')
sha256sums=('SKIP'
            'a35af3210bff1a8f2c04add47ed131a17df9eafbbc6f1671f97021be3ab1ff34')

build() {
	cd "$srcdir"/polkit #-$pkgver"

	NOCONFIGURE=1 ./autogen.sh
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--localstatedir=/var \
		--libexecdir=/usr/lib/polkit-1 \
		--enable-libsystemd-login=no \
		--disable-static \
		--enable-gtk-doc
	make
}

package() {
	cd "$srcdir"/polkit #-$pkgver"
	make DESTDIR="$pkgdir" install

	chown 102 "$pkgdir/etc/polkit-1/rules.d"
	chown 102 "$pkgdir/usr/share/polkit-1/rules.d"

	install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"
}
