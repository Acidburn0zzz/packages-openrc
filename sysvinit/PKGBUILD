# Maintainer: udeved <udeved@openrc4arch.site40.net>

pkgname=sysvinit
pkgver=2.88
pkgrel=13
pkgdesc="Linux System V Init"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
depends=('sysvinit-tools')
conflicts=('openrc-sysvinit')
install=${pkgname}.install
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}dsf.tar.bz2")
sha256sums=('60bbc8c1e1792056e23761d22960b30bb13eccc2cabff8c7310a01f4d5df1519')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}dsf"
	make
}

package(){
 	cd "${srcdir}/${pkgname}-${pkgver}dsf"
 	make ROOT="${pkgdir}" install

 	cd ${pkgdir}

 	rm ${pkgdir}/bin/{pidof,mountpoint}

 	rm ${pkgdir}/sbin/{bootlogd,fstab-decode,killall5,sulogin}
	rm ${pkgdir}/usr/share/man/man8/{bootlogd,fstab-decode,killall5,pidof,sulogin}.8

	rm -r ${pkgdir}/usr/include

	rm ${pkgdir}/usr/bin/{last,lastb,mesg,utmpdump,wall}
	rm -r ${pkgdir}/usr/share/man/man1

 	mv ${pkgdir}/sbin/* ${pkgdir}/usr/bin/
	rm -r ${pkgdir}/sbin
	rm -r ${pkgdir}/bin
}
