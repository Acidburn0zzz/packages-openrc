# Maintainer: artoo <flower_of_life@gmx.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=213
#_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"

pkgbase=eudev
pkgname=('eudev' 'libeudev' 'eudev-openrc')
pkgver=1.7
pkgrel=4
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
groups=('eudev-base')
makedepends=('acl' 'dbus' 'hwids' 'kbd' 'kmod' 'pam'
	    'util-linux' 'gobject-introspection'
	    'gperf' 'gtk-doc' 'intltool')
options=(!makeflags !libtool)
source=("${url}/${pkgbase}-${pkgver}.tar.gz"
	'initcpio_hooks'
	'initcpio_install'
	#"${_gentoo_uri}/sys-fs/eudev/files/udev-postmount")
	'udev-postmount')

noextract=("libsystemd-214-2-${CARCH}.pkg.tar.xz")

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
		--with-modprobe=/usr/bin/modprobe \
		--with-html-dir=/usr/share/doc/${pkgbase}-${pkgver}/html \
		--enable-doc \
		--enable-gtk-doc \
		--enable-gudev \
		--enable-introspection \
		--enable-split-usr \
		--enable-rule-generator
	make
}

package_eudev() {
  pkgdesc="The userspace dev tools (udev) forked by Gentoo"
  license=('GPL')
  depends=('glib2' 'kbd' 'kmod' 'hwids'
	    'libsystemd-eudevcompat' 'pam' 'util-linux')
  optdepends=('eudev-openrc: eudev-postmount script'
			'dbus-eudev: dbus without systemd'
			'upower-pm-utils-eudev: upower without systemd'
			'udisks2-eudev: udisks2 without systemd'
			'libpulse-eudev: libpulse without systemd'
			'cups-eudev: cups without systemd'
			'procps-ng-eudev: procps-ng without systemd')
  provides=("udev=${_udevver}"
	    "systemd=${_udevver}"
	    "systemd-tools=${_udevver}")
  conflicts=('systemd'
	    'systemd-tools')
  backup=('etc/udev/udev.conf')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
		s#GROUP="tape"#GROUP="storage"#g;
		s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"

  # split out runtime libraries
  rm -rf "$srcdir/_libeudev"
  install -dm755 "$srcdir"/_libeudev/usr/lib
  cd "$srcdir"/_libeudev
  mv "$pkgdir"/usr/lib/lib{gudev,udev}*.so* usr/lib
}

package_libeudev() {
	pkgdesc="eudev client libraries"
	depends=('glib2' 'glibc')
	license=('GPL2')
	provides=('libgudev-1.0.so' 'libudev.so'
			"libeudev=${_udevver}")
	conflicts=('libsystemd.so' 'libsystemd-daemon.so' 'libsystemd-id128.so'
				'libsystemd-journal.so' 'libsystemd-login.so')

	mv "$srcdir/_libeudev"/* "$pkgdir"

}

package_eudev-openrc(){
	pkgdesc="OpenRC eudev postmount script"
	arch=('any')
	groups=('openrc' 'openrc-sys' 'eudev-base')
	depends=('openrc-base' 'eudev')
	conflicts=('openrc-arch-services-git'
				'initscripts'
				'systemd-sysvcompat'
				'openrc'
				'openrc-git')
	install=eudev.install

	install -Dm755 "${srcdir}/udev-postmount" "${pkgdir}/etc/init.d/udev-postmount"

	local _shebang='s|#!/sbin/runscript|#!/usr/bin/runscript|'
	sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/udev-postmount"
}
