# $Id: PKGBUILD 197835 2013-10-30 11:06:16Z allan $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=openvpn-eudev
pkgver=2.3.2
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network)"
arch=(i686 x86_64)
url="http://openvpn.net/index.php/open-source.html"
depends=('openssl' 'lzo2' 'iproute2')
makedepends=('udev')
optdepends=('openvpn-openrc: opencpn openrc initscript')
provides=('openvpn')
conflicts=('openvpn')
license=('custom')
backup=('usr/share/openvpn/easy-rsa/vars'
        'usr/share/openvpn/easy-rsa/openssl-1.0.0.cnf')
source=(http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz
        http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz.asc
        http://build.openvpn.net/downloads/releases/easy-rsa-2.2.0_master.tar.gz
        http://build.openvpn.net/downloads/releases/easy-rsa-2.2.0_master.tar.gz.asc)


build() {
  cd $srcdir/openvpn-$pkgver
  # Build openvpn
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-password-save \
    --mandir=/usr/share/man \
    --enable-iproute2 \
    --disable-systemd
  make
  # Build easy-rsa
  cd $srcdir/easy-rsa-2.2.0_master
  ./configure --prefix=/usr --with-easyrsadir=/usr/share/openvpn/easy-rsa
  make
}

package() {
  cd $srcdir/openvpn-$pkgver
  # Install openvpn
  make DESTDIR=$pkgdir install
  install -d -m755 $pkgdir/etc/openvpn
  # Install examples
  install -d -m755 $pkgdir/usr/share/openvpn
  cp -r sample/sample-config-files $pkgdir/usr/share/openvpn/examples
  # Install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/openvpn/COPYING
  # Install contrib
  install -d -m755 $pkgdir/usr/share/openvpn/contrib
  cp -r contrib $pkgdir/usr/share/openvpn
  # Install easy-rsa
  cd $srcdir/easy-rsa-2.2.0_master
  make install DESTDIR=$pkgdir
  rm -f ${pkgdir}/usr/share/openvpn/easy-rsa/openssl-0.9.?.cnf
}
