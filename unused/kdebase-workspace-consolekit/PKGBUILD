# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace-consolekit
_pkgname=kde-workspace
pkgver=4.11.9
_kdever=4.12.5
pkgrel=3
pkgdesc="kdebase-workspace with ConsoleKit support for non-systemd systems"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=("kdelibs>=${pkgver}" 'kdepim-runtime' 'lm_sensors' 'libraw1394'
         'qimageblitz' 'polkit-kde' 'xorg-xprop' 'libxdamage' 'libqalculate'
         'libxklavier' 'xorg-xsetroot' 'libxcomposite' 'libxinerama'
         'xorg-xrdb' 'libxres' 'xorg-xrandr' 'xorg-xmessage' 'libusb-compat'
         'kde-base-artwork' 'xcb-util-renderutil' 'xcb-util-image' 'ttf-font'
         'xcb-util-keysyms' 'xcb-util-wm' 'pciutils' 'polkit-consolekit'
         'consolekit' 'glu')
makedepends=('cmake' 'automoc4' 'boost' 'kdebindings-python2' 'networkmanager')
optdepends=('kde-wallpapers: wallpapers for KDE Plasma Workspaces'
            'appmenu-qt: menu applications over dbus')
provides=("kdebase-workspace=$pkgver")
conflicts=('kdebase-workspace')
install="kdebase-workspace.install"
backup=('usr/share/config/kdm/kdmrc')
source=("http://download.kde.org/stable/${_kdever}/src/${_pkgname}-${pkgver}.tar.xz"
	'kde.pam' 'kde-np.pam' 'kscreensaver.pam' 'kdm.logrotate'
	'etc-scripts.patch' 'terminate-server.patch' 'kdm-xinitrd.patch'
	'khotkeys-qt4.patch' 'kdm-desktopnames.patch')

# avoid linking error when libsystemd-journal.so.0 doesn't exist in
# user's system
check_libpulse() {
	if ! pacman -Q systemd >/dev/null 2>&1 && \
		pacman -Q libpulse >/dev/null 2>&1; then
		return 1
	fi
	return 0
}
check_libpulse || makedepends+=('libpulse-eudev')

prepare() {
	cd ${_pkgname}-${pkgver}

	# reads the shell scripts in /etc/kde/
	patch -p0 -i "${srcdir}"/etc-scripts.patch
	# FS#26120
	patch -p1 -i "${srcdir}"/kdm-xinitrd.patch
	# FS#39188
	patch -p1 -i "${srcdir}"/khotkeys-qt4.patch

	# KDEBUG#202629
	patch -p0 -i "${srcdir}"/terminate-server.patch

	# KDEBUG#334159
	patch -p1 -i "${srcdir}"/kdm-desktopnames.patch

	cd ../
}

build() {
	mkdir build
	cd build
	cmake ../${_pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_libgps=OFF \
		-DPYTHON_EXECUTABLE=/usr/bin/python2 \
		-DWITH_CkConnector=ON \
		-DWITH_NepomukCore=OFF \
		-DWITH_Soprano=OFF
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	install -D -m644 "${srcdir}"/kde.pam "${pkgdir}"/etc/pam.d/kde
	install -D -m644 "${srcdir}"/kde-np.pam "${pkgdir}"/etc/pam.d/kde-np
	install -D -m644 "${srcdir}"/kscreensaver.pam "${pkgdir}"/etc/pam.d/kscreensaver

	install -d -m755 "${pkgdir}"/usr/share/xsessions/
	ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop \
		"${pkgdir}"/usr/share/xsessions/
	install -d -m755 "${pkgdir}"/etc/kde/{env,shutdown}

	install -d -g 135 -o 135 "${pkgdir}"/var/lib/kdm
	#install -D -m755 "${srcdir}"/kdm "${pkgdir}"/etc/rc.d/kdm
	#install -D -m644 "${srcdir}"/kdm.service \
	#	"${pkgdir}"/usr/lib/systemd/system/kdm.service
	install -Dm644 "${srcdir}"/kdm.logrotate "${pkgdir}"/etc/logrotate.d/kdm
}
sha256sums=('aa3d12a264e7fcb6c1330e800b9c7d43c00277275d182824d42f9fe1def0c0cf'
            '812111f166fdf9ab7ef9400c4414cbe26fac5210f2292399d4446fe6e89eea54'
            '3ca72a5f34d29747d12b9b41888d0e369956ba383478850c578e3aa109444214'
            '0a985e3b699578de3e22b25ee72abe4e24ca8730e4afb37dfeed367823bdb0d5'
            '6fdf115bd6b6cf15dddbe3e0eab00fb62a296d5292759f1a09dcb865a357c220'
            'a4c05882dd3a340960dec495cb2fd19cf2523d14a7add5b77af0f98cdff4c394'
            'e5329c87f14ce04f80291f1f7a2be47994f3d54425b572c308b38e2718390959'
            'df7fe57d73c5a017fed698976b00e6488b8c2f555ff9411e55c015cbcef3312b'
            '265bdd90af513f21a1398bda1fcdf4a347ba6279cd42590d43a2226a593de070'
            '6d6f92ca491f04ad752d968f2624ba9f1d668ae8aedfa78e0522db8d9a224f86')
