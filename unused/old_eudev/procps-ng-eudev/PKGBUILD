# Maintainer: artoo <flower_of_life@gmx.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_pkgname=procps-ng

pkgname=procps-ng-eudev
pkgver=3.3.9
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='http://sourceforge.net/projects/procps-ng/'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
groups=('eudev-base')
provides=('procps' "procps-ng=$pkgver" 'sysvinit-tools')
depends=('ncurses')
conflicts=('procps' 'procps-ng' 'sysvinit-tools' 'sysctl-default-conf' 'procps-ng-nosystemd')
replaces=('procps' 'procps-ng' 'sysvinit-tools' 'sysctl-default-conf' 'procps-ng-nosystemd')
install=${_pkgname}.install
source=("http://downloads.sourceforge.net/project/procps-ng/Production/${_pkgname}-${pkgver}.tar.xz"
        'sysctl.conf')
sha1sums=('088c77631745fc75ee41fc29c254a4069be4869a'
          '674282245d8ab2e09017b8f8cdce20a3ff81e631')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--enable-watch8bit
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# Is this needed? Arch removed it, all config is done in /etc/sysctl.d
	install -Dm644 "sysctl.conf" "$pkgdir/etc/sysctl.conf"

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
