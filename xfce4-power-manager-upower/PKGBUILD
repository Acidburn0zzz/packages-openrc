# $Id$
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>
# Maintainer: Aaditya Bagga <aaditya_gnulinux@zoho.com>

pkgname=xfce4-power-manager-upower
_pkgname=xfce4-power-manager
pkgver=1.4.3
pkgrel=2
pkgdesc="Power manager for Xfce desktop, with upower 0.9.23"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('xfce4-panel>=4.12' 'upower=0.9.23' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool')
conflicts=('xfce4-power-manager' 'xfce4-power-manager-upower0' 'xfce4-power-manager-pmu')
provides=('xfce4-power-manager')
replaces=('xfce4-power-manager-pmu')
install=${_pkgname}.install
source=(http://archive.xfce.org/src/apps/$_pkgname/1.4/$_pkgname-$pkgver.tar.bz2)
sha256sums=('018e11dd315745f582d70108b6071d37fb94854fde70961e17325bc19c4e2011')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-network-manager \
    --enable-polkit \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
